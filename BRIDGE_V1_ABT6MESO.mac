! BRIDGE_V1_ABT6MESO , abtCS , girder_y
! BRIDGE_V1_ABT6MESO , ARG1  , ARG2

! Dependencies:
!   GEO_ABT_postHeight
!   GEO_ABT_height
!   GEO_ABT_girderH
!   GEO_ABT_girderB
!   GEO_ABT_width
!   GEO_ABT_columnSpacing
!
!   GIRDER_d
!   SEC_ABT_thinTranWall_t


_abtCS = ARG1

girderHeight  = GIRDER_d(1)  ! TODO  hard-coded, improve
frameGirderZ  = -(girderHeight+GEO_ABT_postHeight+(GEO_ABT_girderH/2))
columnBottomZ = frameGirderZ-GEO_ABT_height
postTopZ      = frameGirderZ+(GEO_ABT_girderH/2)+GEO_ABT_postHeight
wallHeight    = girderHeight+GEO_ABT_postHeight+(GEO_ABT_girderH/2)
wallOffset    = (GEO_ABT_girderB-SEC_ABT_thinTranWall_t)/2

*get,girderCount,parm,%ARG2%,dim,x

! Prepare environment
csys,_abtCS

! Draw wall
asel,none
wpcsys,-1
wpoffs,wallOffset
wprota,,,-90
wpoffs,,-GEO_ABT_width/2
blc4,0,0,wallHeight,GEO_ABT_width
cm,cma_cantileverWall,area

! Draw frame girder
lsel,none
nextkp,'frame_girder_kps',2
k , frame_girder_kps(1) , 0 , -GEO_ABT_width , 0
k , frame_girder_kps(2) , 0 , GEO_ABT_width  , 0
l,frame_girder_kps(1),frame_girder_kps(2)
cm,cml_frameGirder,line

! Draw posts
*do,nthPost,1,girderCount,1
  yGirderGlobal = %ARG2%(nthGirder)
  glo2loc,0,yGirderGlobal,0,_abtCS,,'yGirderLocal'
  nextkp,'post_kps',2
  lsel,none
  k,post_kps(1),0,yGirderLocal,frameGirderZ
  k,post_kps(2),0,yGirderLocal,postTopZ
  l,post_kps(1),post_kps(2)
  cmpadd,'cml_posts'
*enddo

! Draw columns  TODO  currently hard-coded to always work with 2 columns
lsel,none
nextkp,'column_kps',4
k , column_kps(1) , 0 , -GEO_ABT_columnSpacing/2 , frameGirderZ
k , column_kps(2) , 0 , -GEO_ABT_columnSpacing/2 , columnBottomZ
k , column_kps(3) , 0 ,  GEO_ABT_columnSpacing/2 , frameGirderZ
k , column_kps(4) , 0 ,  GEO_ABT_columnSpacing/2 , columnBottomZ
l , column_kps(1) , column_kps(2)
l , column_kps(3) , column_kps(4)
cmpadd,'cml_columns'
