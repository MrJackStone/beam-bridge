! BRIDGE_V1_SMPCAP , nthSupport
! BRIDGE_V1_SMPCAP , ARG1


! Dependencies:
!   GEO_capLength
!   GEO_capOpt
!   GEO_capWidth
!   XSliceCount
!   YSliceCount
!   GEO_columnWidth
!
!   GEO_xCapSlice
!   GEO_yCapSlice

!   GEO_deckLength
!   GEO_deckOff_beg
!   GEO_deckOff_end
!   GEO_deckRadius
!   GEO_deckWidth_beg
!   GEO_chordOff
!
!   GEO_columnY_<i>
!   GEO_frameBotOffset
!   GEO_frameTopOffset
!   GEO_supportChi
!   GEO_supportX

! Exported:
!   CS_INFRA_C<i>
!   CS_INFRA_L<i>
!   CS_INFRA_R<i>
!
!   COMPL_GradeBeam
!   COMPA_PileCap


xCenter = GEO_supportX(arg1)
*if,GEO_deckRadius,eq,0,then
  ! Straight deck:
  xLeft  = xCenter
  xRight = xCenter

  yCenter = GEO_deckOff_beg+(GEO_deckOff_end-GEO_deckOff_beg)*(xCenter/GEO_deckLength)+(GEO_deckWidth_beg/2)
  yLeft   = yCenter+GEO_columnY_%arg1%(1)
  yRight  = yCenter+GEO_columnY_%arg1%(2)

  zAngle = 0

*else
  ! Curved deck:
  *afun,deg
  chi_i = GEO_supportChi(arg1)

  xLeft  = (GEO_deckLength/2)-(GEO_deckRadius+GEO_columnY_%arg1%(1))*cos(chi_i)
  xRight = (GEO_deckLength/2)-(GEO_deckRadius+GEO_columnY_%arg1%(2))*cos(chi_i)

  yCenter = GEO_deckRadius*sin(chi_i)-GEO_chordOff
  yLeft   = (GEO_deckRadius+GEO_columnY_%arg1%(1))*sin(chi_i)-GEO_chordOff
  yRight  = (GEO_deckRadius+GEO_columnY_%arg1%(2))*sin(chi_i)-GEO_chordOff

  zAngle = 90-chi_i
  *afun,rad
*endif

! Top and bottom Z coordinates of the frame
nthFrameZTop = zFrameTop+GEO_frameTopOffset(arg1)
nthFrameZBot = zFrameBot+GEO_frameBotOffset(arg1)

CS_center = 41+(10*arg1)
CS_left   = CS_center+1
CS_right  = CS_center+2

cCS_name = strcat('CS_INFRA_C',chrval(arg1))
lCS_name = strcat('CS_INFRA_L',chrval(arg1))
rCS_name = strcat('CS_INFRA_R',chrval(arg1))

%cCS_name% = CS_center
%lCS_name% = CS_left
%rCS_name% = CS_right

local , CS_center , cart , xCenter , yCenter , nthFrameZBot , zAngle  ! TODO  csys
local , CS_left   , cart , xLeft   , yLeft   , nthFrameZBot , zAngle  ! TODO  csys
local , CS_right  , cart , xRight  , yRight  , nthFrameZBot , zAngle  ! TODO  csys
csys  , 0

*get , XSliceCount , parm , GEO_xCapSlice , dim , x
*get , YSliceCount , parm , GEO_yCapSlice , dim , x

*if,GEO_capOpt,eq,0,then
  ! Two pile caps connected by grade beam:

  ! Left pile cap
  csys,CS_left
  wpcsys,,CS_left

  ksel , s , loc , x , 0
  ksel , r , loc , y , 0
  ksel , r , loc , z , 0
  *get,KNum,kp,,num,min

  asel,none
  blc5,0,0,GEO_capWidth,GEO_capLength

  *do,nthX,1,XSliceCount,1
    ! Slice pile cap: YZ plane
    xOff = GEO_xCapSlice(nthX)
    wpoffs,xOff
    wprota,,,90
    asbw,all,,delete
    wprota,,,-90
    wpoffs,-xOff
  *enddo

  *do,nthY,1,YSliceCount,1
    ! Slice pile cap: XZ plane
    yOff = GEO_yCapSlice(nthY)
    wpoffs,,yOff
    wprota,,90
    asbw,all,,delete
    wprota,,-90
    wpoffs,,-yOff
  *enddo

  cmpadd,'COMPA_PileCap'

  ! Right pile cap
  csys,CS_right
  wpcsys,,CS_right

  ksel,s,loc,x,0
  ksel,r,loc,y,0
  ksel,r,loc,z,0
  *get,KNum,kp,,num,min

  asel,none
  blc5,0,0,GEO_capWidth,GEO_capLength

  *do,nthX,1,XSliceCount,1
    ! Slice pile cap: YZ plane
    xOff = GEO_xCapSlice(nthX)
    wpoffs,xOff
    wprota,,,90
    asbw,all,,delete
    wprota,,,-90
    wpoffs,-xOff
  *enddo

  *do,nthY,1,YSliceCount,1
    ! Slice pile cap: XZ plane
    yOff = GEO_yCapSlice(nthY)
    wpoffs,,yOff
    wprota,,90
    asbw,all,,delete
    wprota,,-90
    wpoffs,,-yOff
  *enddo

  cmpadd,'COMPA_PileCap'

  ! Create grade beam
  csys,CS_center
  wpcsys,,CS_center
  ksel,s,loc,x,0
  ksel,r,loc,y,yLeft+(GEO_capLength/2)
  ksel,r,loc,z,0
  *get,KLeft,kp,,num,min
  ksel,s,loc,x,0
  ksel,r,loc,y,yRight-(GEO_capLength/2)
  ksel,r,loc,z,0
  *get,KRight,kp,,num,min
  ksel,s,kp,,KLeft
  ksel,a,kp,,KRight
  lsel,none
  L,KLeft,KRight
  cmpadd,'COMPL_GradeBeam'

*elseif,GEO_capOpt,eq,1,then
  ! Single pile cap for both columns

  ! Draw pile cap
  csys,CS_center
  wpcsys,,CS_center
  asel,none
  blc5,0,0,GEO_capWidth,GEO_capLength

  ! Slice cap
  batch_sliceyz,'GEO_xCapSlice',1
  batch_slicexz,'GEO_yCapSlice',1

  *if,GEO_frameOpt,eq,1,then
    ! Tubular pier column: slice at superstructure positions
    *del,XZslice,,nopr
    *dim,XZslice,,2
    XZslice(1)=-GEO_columnWidth/2,GEO_columnWidth/2
    batch_slicexz,'XZslice',1
    *if,GEO_deckRadius,eq,0,then
      BRIDGE_V1_LONGSLICE
    *else
      BRIDGE_V1_ARCSLICE , 1
    *endif

  *elseif,GEO_frameOpt,eq,3,then
    ! Hammerhead pier: slice around solid circular column
    BRIDGE_V1_SLICEAROUNDBEAM ,  , GEO_pierColumnShape , GEO_pierColumnDiameter , GEO_pierColumnCx , GEO_pierColumnCy , 'COMPA_HAMMER%ARG1%_botInterface'

  *endif

  cmpadd,'COMPA_PileCap'

*endif
