! BRIDGE_V1_SMPCAP , supIdx , capOpt , capLx , capLy , x_slices , y_slices , cmInfix
! BRIDGE_V1_SMPCAP , ARG1   , ARG2   , ARG3  , ARG4  , ARG5     , ARG6     , ARG7


! Dependencies:
!   CS_INFRA_C<i>
!   CS_INFRA_L<i>
!   CS_INFRA_R<i>

! Exported:
!   COMPL_<cmInfix_>GradeBeam
!   COMPA_<cmInfix_>PileCap


! Parse inputs
_supIdx   = ARG1
_capOpt   = ARG2
_capLx    = ARG3
_capLy    = ARG4
_x_slices = ARG5
_y_slices = ARG6
_cmInfix  = ARG7

infix = ''
*get,parType,parm,_cmInfix,type
*if,parType,eq,3,then
  infix = strcat(_cmInfix,'_')
*endif

! Prepare environment
lsel , none
asel , none
cm   , cma_pileCap      , area
cm   , cma_pileCapLeft  , area
cm   , cma_pileCapRight , area
cm   , cml_gradeBeam    , line

! Compile slicing positions
arruniq , _x_slices , 'x_slices' , 1
arruniq , _y_slices , 'y_slices' , 1

*if,_capOpt,eq,0,then
  ! Two pile caps connected by grade beam:

  ! Left & right pile caps
  BRIDGE_V1_BLC5SLICED , CS_INFRA_l%_supIdx% , _capLy , _capLx , 'x_slices' , 'y_slices' , 'cma_pileCapLeft'
  BRIDGE_V1_BLC5SLICED , CS_INFRA_r%_supIdx% , _capLy , _capLx , 'x_slices' , 'y_slices' , 'cma_pileCapRight'
  cmsel,s,cma_pileCapLeft
  cmsel,a,cma_pileCapRight
  cm,cma_pileCap,area

  ! Create grade beam
  BRIDGE_V1_GRADEBEAM , CS_INFRA_c%_supIdx% , 'cma_pileCapLeft' , 'cma_pileCapRight' , 'cml_gradeBeam'

*elseif,_capOpt,eq,1,then
  ! Single pile cap for both columns:
  BRIDGE_V1_BLC5SLICED , CS_INFRA_c%_supIdx% , _capLy , _capLx , 'x_slices' , 'y_slices' , 'cma_pileCap'

*endif

! Export selection components
cmsel,s,cma_pileCap
cmpadd,'COMPA_%infix%pileCap'

cmsel,s,cml_gradeBeam
cmpadd,'COMPL_%infix%gradeBeam'

! Clean up
cmdele , cma_pileCap
cmdele , cma_pileCapLeft
cmdele , cma_pileCapRight
cmdele , cml_gradeBeam
