! BRIDGE_V1_SMPCAP , nthSupport
! BRIDGE_V1_SMPCAP , ARG1


! Dependencies:
!   CS_INFRA_C<i>
!   CS_INFRA_L<i>
!   CS_INFRA_R<i>
!
!   GEO_ABT_capLx
!   GEO_ABT_capOpt

! Exported:
!   COMPL_ABT_GradeBeam
!   COMPA_ABT_PileCap


! Calculate pile coordinates in local pile cap CSYS
BRIDGE_V1_CALCPILECOORDS , 'x_piles' , 'y_piles' , GEO_ABT_pileCount , GEO_ABT_pileConfig , GEO_ABT_pileD , GEO_ABT_pilePitchX , GEO_ABT_pilePitchY , GEO_ABT_columnCx , GEO_ABT_columnCy , GEO_ABT_capLx , GEO_ABT_capLy

! Calculate slicing positions
zero         = 0
leftColumnY  = -GEO_ABT_columnSpacing/2
rightColumnY = GEO_ABT_columnSpacing/2

arrjoin , 'x_temp' , 'x_piles'  , 'zero'
arruniq , 'x_temp' , 'x_slices' , 1

arrjoin , 'y_temp' , 'y_piles'  , 'zero' , 'leftColumnY' , 'rightColumnY'
arruniq , 'y_temp' , 'y_slices' , 1

*if,GEO_ABT_capOpt,eq,0,then
  ! Two pile caps connected by grade beam:

  ! Left & right pile caps
  BRIDGE_V1_BLC5SLICED , CS_INFRA_l%ARG1% , GEO_ABT_capLy , GEO_ABT_capLx , 'x_slices' , 'y_slices' , 'cma_pileCapLeft'
  BRIDGE_V1_BLC5SLICED , CS_INFRA_r%ARG1% , GEO_ABT_capLy , GEO_ABT_capLx , 'x_slices' , 'y_slices' , 'cma_pileCapRight'
  cmsel,s,cma_pileCapLeft
  cmsel,a,cma_pileCapRight
  cm,cma_pileCap,area

  ! Create grade beam
  BRIDGE_V1_GRADEBEAM , CS_INFRA_c%ARG1% , 'cma_pileCapLeft' , 'cma_pileCapRight' , 'cml_gradeBeam'

*elseif,GEO_ABT_capOpt,eq,1,then
  ! Single pile cap for both columns:
  BRIDGE_V1_BLC5SLICED , CS_INFRA_c%ARG1% , GEO_ABT_capLy , GEO_ABT_capLx , 'x_slices' , 'y_slices' , 'cma_pileCap'

*endif

! Export selection components
cmsel,s,cma_pileCap
cmpadd,'COMPA_ABT_pileCap'

cmsel,s,cml_gradeBeam
cmpadd,'COMPL_ABT_GradeBeam'
