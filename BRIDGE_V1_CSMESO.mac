! BRIDGE_V1_CSMESO, nthSupport
! BRIDGE_V1_CSMESO,       arg1


CS_name=strcat('CS_MESO_',chrval(arg1))

exists=0
*get,parType,parm,%CS_name%,type
*if,parType,eq,0,then
  ! Parameter defined: check if corresponding coordinate system exists
  *if,csyiqr(%CS_name%,1),eq,1,then
    exists=1
  *endif
*endif

*if,exists,eq,1,then
  ! Coordinate system exists: make it current
  csys,%CS_name%

*else
  ! Coordinate system does not exist: create and make current
  xCenter=GEO_supportX(arg1)
  yOffset=GEO_deckOff_beg+(GEO_deckOff_end-GEO_deckOff_beg)*(xCenter/GEO_deckLength)

  *if,GEO_deckRadius,eq,0,then
    ! Straight deck:
    yCenter = yOffset+(GEO_deckWidth_beg/2)
    zAngle  = 0
  *else
    ! Curved deck:
    *afun,deg
    chi_i=GEO_supportChi(arg1)
    yCenter=GEO_deckRadius*sin(chi_i)-GEO_chordOff
    zAngle=90-chi_i
    *afun,rad
  *endif

  ! TODO: get rid of this hardcoded 40, check for CSYS conflicts elsewhere
  CS_num=40+(10*arg1)
  %CS_name%=CS_num
  csys,0
  local,CS_num,cart,xCenter,yCenter,0,zAngle
  csys,CS_num

*endif
