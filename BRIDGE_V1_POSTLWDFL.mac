! BRIDGE_V1_POSTLWDFL, abtNum
! BRIDGE_V1_POSTLWDFL, arg1

!! Initial configs                                                                                !!
windefa,1
erase
/device,font,3,Arial,1000,0,-64,0,0,,,
/device,font,1,Arial,1000,0,-42,0,0,,,
/device,text,1,100
/tspec,15,1.5,4,0,0
/gformat,f,10,2
/annot,dele

pathCount=5

height = GEO_ABT%arg1%_height
length = GEO_ABT%arg1%_length

cmsel,s,COMPA_ABT_longWalls
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active
csys,CS_ABT%arg1%
*get,yLocalLeft,node,,mnloc,y
*get,yLocalRight,node,,mxloc,y
csys,0

xStep=length/(pathCount-1)
zLocalTop=0
zLocalBot=zLocalTop-height

*del , left_path_names  ,      , nopr
*del , right_path_names ,      , nopr
*dim , left_path_names  , char , pathCount*2
*dim , right_path_names , char , pathCount*2

!! Create paths                                                                                   !!
*do,nthPath,1,pathCount,1
  xLocal=xStep*(nthPath-1)

  loc2glo,xLocal,yLocalLeft,zLocalBot,CS_ABT%arg1%,'x1Lg','y1Lg','z1Lg'
  loc2glo,xLocal,yLocalLeft,zLocalTop,CS_ABT%arg1%,'x2Lg','y2Lg','z2Lg'

  loc2glo,xLocal,yLocalRight,zLocalBot,CS_ABT%arg1%,'x1Rg','y1Rg','z1Rg'
  loc2glo,xLocal,yLocalRight,zLocalTop,CS_ABT%arg1%,'x2Rg','y2Rg','z2Rg'

  leftPathName='LWL_P%nthPath%'
  rightPathName='LWR_P%nthPath%'
  left_path_names(nthPath)=leftPathName
  right_path_names(nthPath)=rightPathName

  path,leftPathName,2,30,50
  ppath,1,,x1Lg,y1Lg,z1Lg
  ppath,2,,x2Lg,y2Lg,z2Lg
  pdef,'UY',u,y,avg

  path,rightPathName,2,30,50
  ppath,1,,x1Rg,y1Rg,z1Rg
  ppath,2,,x2Rg,y2Rg,z2Rg
  pdef,'UY',u,y,avg
*enddo

!! Calculate path envelopes                                                                       !!
*del , left_lwall_envelope  ,      , nopr
*del , right_lwall_envelope ,      , nopr
*dim , left_lwall_envelope  , char , 2    , pathCount
*dim , right_lwall_envelope , char , 2    , pathCount
*do,nthPath,1,pathCount,1
  left_lwall_envelope(1,nthPath)  = left_path_names(nthPath)
  left_lwall_envelope(2,nthPath)  = 'DFL_FRQ'
  right_lwall_envelope(1,nthPath) = right_path_names(nthPath)
  right_lwall_envelope(2,nthPath) = 'DFL_FRQ'
*enddo

pthenv,'CLEAR','left_lwall_envelope'
pthenv,'CLEAR','right_lwall_envelope'

cmsel,s,COMPA_ABT_longWalls
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active

*do,nthLS,1,LoadStepCount,1

  subset,nthLS,last

  *do,nthPath,1,pathCount,1
    pathName='LWL_P%nthPath%'
    path,pathName
    pdef      , 'UY_frq' , etab , 'UY_frq'  , 1
    pthrebase , 'UY_frq' , 1    , 'DFL_FRQ' , 1

    pathName='LWR_P%nthPath%'
    path,pathName
    pdef      , 'UY_frq' , etab , 'UY_frq'  , 1
    pthrebase , 'UY_frq' , 1    , 'DFL_FRQ' , 1
  *enddo

  pthenv,'UPDATE','left_lwall_envelope'
  pthenv,'UPDATE','right_lwall_envelope'
*enddo

!! Plot path envelopes                                                                            !!
*del , envelope_left_path_names  ,      , nopr
*dim , envelope_left_path_names  , char , pathCount*2
*del , envelope_right_path_names ,      , nopr
*dim , envelope_right_path_names , char , pathCount*2
*del , envelope_path_labels      ,      , nopr
*dim , envelope_path_labels      , char , pathCount*2

*do,nthPath,1,pathCount,1
  envelope_left_path_names(nthPath*2-1)  = left_path_names(nthPath)
  envelope_left_path_names(nthPath*2)    = left_path_names(nthPath)
  envelope_right_path_names(nthPath*2-1) = right_path_names(nthPath)
  envelope_right_path_names(nthPath*2)   = right_path_names(nthPath)
  envelope_path_labels(nthPath*2-1)      = 'DFL_FRQ+'
  envelope_path_labels(nthPath*2)        = 'DFL_FRQ-'
*enddo

envelopeLeftPathNamesStr  = 'envelope_left_path_names'
envelopeRightPathNamesStr = 'envelope_right_path_names'
envelopePathLabelsStr     = 'envelope_path_labels'
leftNamesContainer        = 'envelopeLeftPathNamesStr'
rightNamesContainer       = 'envelopeRightPathNamesStr'
labelContainer            = 'envelopePathLabelsStr'

view2w
/window,3,-1,1.67,-1,1
wincln,1
wincln,2
wincln,3

wintitle , 3 , 'ENCONTRO 0%arg1%:' , 'PAREDES LATERAIS' , 'FLECHA FREQUENTE [mm]',,1
/window,3,off

*if,arg1,eq,1,then
  /view,1,0,1,0
  /view,2,0,-1,0
*elseif,arg1,eq,2,then
  /view,1,0,-1,0
  /view,2,0,1,0
*endif

/angle,all,0
/udoc,all,cntr,top
/plopts,leg3,on
/triad,off
/eshape,1
/udoc,all,cntr,top
/edge,all,0

/window,1,on
/window,2,off
cmsel,s,COMPA_ABT_%arg1%
esla,s
nsle,s,active
/auto,1
eplot
/user,1

/window,1,off
/window,2,on
cmsel,s,COMPA_ABT_%arg1%
esla,s
nsle,s,active
/auto,2
eplot
/user,2

cmsel,s,COMPA_ABT_%arg1%
cmsel,u,COMPA_ABT_longWalls
esla,s
nsle,s,active
/window,1,on
/window,2,on

go2png,1200
  /device,vector,on
  eplot
  /noerase
  cmsel,s,COMPL_ABT_piles
  cmsel,r,COMPL_ABT_%arg1%
  esll,a
  nsle,s,active
  eplot

  /device,vector,off
  /eshape,0
  /edge,all,0

  /window,1,on
  /window,2,off
  cmsel,s,COMPA_ABT_longWalls
  cmsel,r,COMPA_ABT_%arg1%
  cmsel,r,COMPA_ABT_rightSide
  esla,s
  nsle,s,active
  eplot
  cleanplot,'mpathplot','10',,'leftNamesContainer','labelContainer'

  /window,1,off
  /window,2,on
  cmsel,s,COMPA_ABT_longWalls
  cmsel,r,COMPA_ABT_%arg1%
  cmsel,r,COMPA_ABT_leftSide
  esla,s
  nsle,s,active
  eplot
  cleanplot,'mpathplot','10',,'rightNamesContainer','labelContainer'

endpng,'IMG21a_ABT%arg1%_LWALL_deflects',,,,,POST_resultsFolder
/erase
/annot,dele
clims
view1w
/window,1,on
