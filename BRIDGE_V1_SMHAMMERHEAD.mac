! BRIDGE_V1_SMHAMMERHEAD , supportIdx
! BRIDGE_V1_SMHAMMERHEAD , ARG1


! Prepare environment
cmaName      = 'COMPA_HAMMER_%ARG1%'
cmlName      = 'COMPL_HAMMER_%ARG1%'
cmkName      = 'COMPK_HAMMER_%ARG1%'
cmaInterface = 'COMPA_HAMMER%ARG1%_topInterface'

csbackup

BRIDGE_V1_CSMESO, ARG1
*get,CS_num,active,,csys

! Calculate top & bottom Z coordinates of the pier
topOffset    = GEO_frameTopOffset(ARG1)
botOffset    = GEO_frameBotOffset(ARG1)
nthFrameZTop = zFrameTop+topOffset
nthFrameZBot = zFrameBot+botOffset

! Draw column
ksel,none
lsel,none
nextkp,'KP_col',2
k , KP_col(1) , 0 , 0 , nthFrameZBot
k , KP_col(2) , 0 , 0 , nthFrameZTop
l,KP_col(1),KP_col(2)
LN_column = lsnext(0)

submergedHeight = GEO_waterHeight-botOffset
*if,submergedHeight,gt,0,then
  relativePosition = submergedHeight/(nthFrameZTop-nthFrameZBot)
  kl,LN_column,relativePosition
*endif
cm,%cmlName%,line
cm,%cmkName%,kp

! Draw pier cap slab
asel,none
wpcsys,-1
wpoffs,,,topOffset
blc5,0,0,GEO_piercapLength,GEO_piercapWidth,0
wprota,,,90
asbw,all,,delete

BRIDGE_V1_SUPERSLICE

cm,%cmaName%,area

wpcsys,-1
BRIDGE_V1_SLICEAROUNDBEAM , cmaName , GEO_pierColumnShape , GEO_pierColumnDiameter , GEO_pierColumnCx , GEO_pierColumnCy , cmaInterface

! Build selection components
cm,%cmaName%,area
cmpadd,'COMPA_hammerPierCap'

ksel,s,kp,,KP_col(1)
cmpadd,'COMPK_hammerColumnBottom'

ksel,s,kp,,KP_col(2)
cmpadd,'COMPK_hammerColumnTop'

cmsel,s,%cmlName%
ksll,s
cmpadd,'COMPL_hammerColumn'
cmpadd,'COMPK_hammerColumn'

! Clean up
*del , CS_num           ,  , nopr
*del , KP_col           ,  , nopr
*del , LN_column        ,  , nopr
*del , cmaName          ,  , nopr
*del , cmkName          ,  , nopr
*del , cmlName          ,  , nopr
*del , relativePosition ,  , nopr
*del , submergedHeight  ,  , nopr

csrestore
