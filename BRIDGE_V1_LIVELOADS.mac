! BRIDGE_V1_LIVELOADS
! BRIDGE_V1_LIVELOADS

! Vehicle traffic uniform load
ALLSEL
CMSEL,S,COMPE_Lanes
CMSEL,R,COMPE_occupied
cmsel,r,COMPE_loaded
cmsel,u,COMPE_LTshadow
NSLE,S,ACTIVE
*get,ECount,ELEM,,COUNT
*if,ECount,GT,0,THEN
   SFE,ALL,2,PRES,,LOAD_vehicleTraffic
*endif

! Pedestrian traffic uniform load
ALLSEL
CMSEL,S,COMPE_LeftFootpath
CMSEL,A,COMPE_RightFootpath
CMSEL,R,COMPE_occupied
cmsel,r,COMPE_loaded
NSLE,S,ACTIVE
*get,ECount,ELEM,,COUNT
*if,ECount,GT,0,THEN
   SFE,ALL,2,PRES,,LOAD_pedestrianTraffic
*endif
ALLSEL

! Water flow uniform load on submerged column elements
*if,GEO_pierCount,gt,0,then
    CMSEL,S,COMPE_Submerged
    *if,GEO_frameOpt,eq,0,then
        SFBEAM,ALL,2,PRES,-(LOAD_waterFlow*COLUMN_cx)
    *elseif,GEO_frameOpt,eq,1,then
        cmsel,s,COMPA_tubeWindFace
        esla,r
        sfe,ALL,1,PRES,,-LOAD_waterFlow
    *endif
*endif

! Water flow on submerged abutment elements
cmsel,s,COMPE_ABT_1
cmsel,a,COMPE_ABT_2
cmsel,r,COMPE_Submerged
cm,COMPE_temp_submerged,elem
*get,ec,elem,,count
*if,ec,gt,0,then
  cmsel,s,COMPA_ABT_longWall
  esla,s
  nsle,s
  nsel,r,loc,y,-1e6,0
  cmsel,s,COMPE_temp_submerged
  esln,r,1
  sfe,all,1,pres,,-LOAD_waterFlow
*endif

! Wind uniform load on exposed column elements
*if,GEO_pierCount,gt,0,then
    CMSEL,S,COMPE_Exposed
    *if,GEO_frameOpt,eq,0,then
        SFBEAM,ALL,2,PRES,-(LOAD_columnWind*COLUMN_cx)
    *elseif,GEO_frameOpt,eq,1,then
        cmsel,s,COMPA_tubeWindFace
        esla,r
        sfe,ALL,1,PRES,,-LOAD_columnWind
    *endif
*endif

! Wind lateral load on the slabs
CMSEL,S,COMPN_Slab
*if,GEO_deckRadius,eq,0,then
    csys,CS_rightDeckEdge
    nsel,r,loc,y,0
    csys,0
*else
    csys,CS_deck
    nsel,r,loc,x,GEO_deckRadius
    csys,0
*endif
*get,NodeCount,NODE,,COUNT
NodalLoad=LOAD_deckWind*(GEO_barrierHeight+GIRDER_d+SLAB_t)*GEO_deckLength/NodeCount
F,ALL,FY,NodalLoad

! Loads on abutments
*if,GEO_ABT1_status,eq,1,or,GEO_ABT2_status,eq,1,then

    ! Vehicle traffic uniform load
    cmsel,s,COMPE_ABT_Lanes
    cmsel,r,COMPE_occupied
    cmsel,r,COMPE_loaded
    cmsel,u,COMPE_LTshadow
    nsle,s,active
    *get,ECount,elem,,count
    *if,ECount,gt,0,then
        sfe,all,2,pres,,LOAD_vehicleTraffic
    *endif

    ! Pedestrian traffic uniform load
    cmsel,s,COMPE_ABT_LeftFootpath
    cmsel,a,COMPE_ABT_RightFootpath
    cmsel,r,COMPE_occupied
    cmsel,r,COMPE_loaded
    nsle,s,active
    *get,ECount,elem,,count
    *if,ECount,gt,0,then
        sfe,all,2,pres,,LOAD_pedestrianTraffic
    *endif

    ! Earth pressures
    BRIDGE_V1_EARTHPRES,2

*endif
