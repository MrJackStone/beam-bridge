! BRIDGE_V1_SMPILE, nthSupport
! BRIDGE_V1_SMPILE, arg1


! Prepare environment
ksel , none
asel , none
cm   , cmk_support , kp
cm   , cmk_pileTop , kp
cm   , cml_pile    , line

! Get pile cap CSYS
*del,pilecap_cs,,nopr
*dim,pilecap_cs,,2

*if,GEO_capOpt,eq,0,then
  ! Two connected pile caps:
  pilecapCount  = 2
  pilecap_cs(1) = CS_INFRA_l%ARG1%, CS_INFRA_r%ARG1%
*elseif,GEO_capOpt,eq,1,then
  ! Single pile cap:
  pilecapCount  = 1
  pilecap_cs(1) = CS_INFRA_c%ARG1%
*endif

! Draw piles
*do,nthCap,1,pilecapCount,1

  nthCS = pilecap_cs(nthCap)

  *do,nthPile,1,GEO_pileCount,1
    ! Calculate pile's top & bottom (X,Y,Z) coordinates
    afunbackup,'DEG'
    pileLen = GEO_pileLengths(ARG1)
    xTop    = GEO_pileXL(nthPile)
    yTop    = GEO_pileYL(nthPile)
    xAngle  = GEO_pileXAngle(nthPile)
    yAngle  = GEO_pileYAngle(nthPile)
    zTop    = 0
    zBot    = zTop-pileLen
    xBot    = xTop+tan(yAngle)*pileLen
    yBot    = yTop+tan(xAngle)*pileLen
    afunrestore

    ! Create pile line
    lsel,none
    csys,nthCS
    nextkp,'pile_ends',2
    k , pile_ends(1) , xTop , yTop , zTop
    k , pile_ends(2) , xBot , yBot , zBot
    l , pile_ends(1) , pile_ends(2)
    cmpadd,'cml_pile'

    ksel,s,kp,,pile_ends(2)
    cmpadd,'COMPK_Support'

    ksel,s,kp,,pile_ends(1)
    cmpadd,'COMPK_PileTop'
  *enddo
*enddo

! Export
cmsel,s,cmk_support
cm,COMPK_%infix%support,kp

cmsel,s,cmk_pileTop
cm,COMPK_%infix%pileTop,kp

cmsel,s,cml_pile
cm,COMPL_%infix%pile,line

! Clean up
cmdele , cmk_support
cmdele , cmk_pileTop
cmdele , cml_pile
