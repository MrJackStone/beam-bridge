! BRIDGE_V1_SMPILE , supIdx , capOpt , pile_data , cmInfix
! BRIDGE_V1_SMPILE , ARG1   , ARG2   , ARG3      , ARG4

! Dependencies:
! CS_INFRA_l<i>
! CS_INFRA_r<i>
! CS_INFRA_c<i>

! 'pile_data' array format:
!   i-pos  param  description
!   1      x      X coordinate at the center of the pile (at the top)
!   2      y      Y coordinate at the center of the pile (at the top)
!   3      L      pile length (vertical projection)
!   4      alpha  angle with the vertical (gravitational) axis (applied second, after beta)
!   5      beta   angle at the horizontal plane (applied first, before alpha)
!   6      id     global pile identification number (optional)

! Parse inputs
_supIdx    = ARG1
_capOpt    = ARG2
_pile_data = ARG3
_cmInfix   = ARG4

infix = ''
*get,parType,parm,_cmInfix,type
*if,parType,eq,3,then
  infix = strcat(_cmInfix,'_')
*endif

! Prepare environment
ksel , none
asel , none
cm   , cmk_support , kp
cm   , cmk_pileTop , kp
cm   , cml_pile    , line

*del,pilecap_cs,,nopr
*dim,pilecap_cs,,2

*get,pileCount,parm,%_pile_data%,dim,y

! Get pile cap CSYS
*if,_capOpt,eq,0,then
  ! Two connected pile caps:
  pilecapCount  = 2
  pilecap_cs(1) = CS_INFRA_l%ARG1%, CS_INFRA_r%ARG1%
*elseif,_capOpt,eq,1,then
  ! Single pile cap:
  pilecapCount  = 1
  pilecap_cs(1) = CS_INFRA_c%ARG1%
*endif

! Draw piles
*do,nthCap,1,pilecapCount,1

  nthCS = pilecap_cs(nthCap)

  *do,nthPile,1,pileCount,1
    ! Collect pile data
    xTop    = %_pile_data%(1,nthPile)
    yTop    = %_pile_data%(2,nthPile)
    pileLen = %_pile_data%(3,nthPile)
    yAngle  = %_pile_data%(4,nthPile)
    xAngle  = %_pile_data%(5,nthPile)

    ! Calculate pile's top & bottom (X,Y,Z) coordinates
    afunbackup,'DEG'
    zTop    = 0
    zBot    = zTop-pileLen
    xBot    = xTop+tan(yAngle)*pileLen
    yBot    = yTop+tan(xAngle)*pileLen
    afunrestore

    ! Create pile line
    lsel,none
    csys,nthCS
    nextkp,'pile_ends',2
    k , pile_ends(1) , xTop , yTop , zTop
    k , pile_ends(2) , xBot , yBot , zBot
    l , pile_ends(1) , pile_ends(2)

    ! Internal components
    cmpadd,'cml_pile'
    ksel,s,kp,,pile_ends(1)
    cmpadd,'cmk_pileTop'
    ksel,s,kp,,pile_ends(2)
    cmpadd,'cmk_support'

  *enddo
*enddo

! Export
cmsel,s,cmk_support
cm,COMPK_%infix%support,kp

cmsel,s,cmk_pileTop
cm,COMPK_%infix%pileTop,kp

cmsel,s,cml_pile
cm,COMPL_%infix%pile,line

! Clean up
cmdele , cmk_support
cmdele , cmk_pileTop
cmdele , cml_pile
