! BRIDGE_V1_SMPILE , supIdx , capOpt , pile_data , capThk , cmaCap , pileD , cmInfix
! BRIDGE_V1_SMPILE , ARG1   , ARG2   , ARG3      , ARG4   , ARG5   , ARG6  , ARG7


! Dependencies:
! CS_INFRA_l<i>
! CS_INFRA_r<i>
! CS_INFRA_c<i>

! 'pile_data' array format:
!   i-pos  param  description
!   1      x      X coordinate at the center of the pile (at the top)
!   2      y      Y coordinate at the center of the pile (at the top)
!   3      L      pile length (vertical projection)
!   4      alpha  angle with the vertical (gravitational) axis (applied second, after beta)
!   5      beta   angle at the horizontal plane (applied first, before alpha)
!   6      id     global pile identification number (optional)


! Parse inputs
_supIdx    = ARG1
_capOpt    = ARG2
_pile_data = ARG3
_cmaCap    = ARG5
_pileD     = ARG6
_cmInfix   = ARG7

infix = ''
*get,parType,parm,_cmInfix,type
*if,parType,eq,3,then
  infix = strcat(_cmInfix,'_')
*endif

zZero = 0
zTop  = 0
*get,parType,parm,ARG4,type
*if,parType,eq,0,and,ARG4,gt,0,then
  ! Apply offset between pile & pile cap if a pile cap thickness is given
  zTop = zZero-ARG4
*endif

sliceCap = 0
*get,parType1,parm,_cmaCap,type
*get,parType2,parm,_pileD,type
*if,parType1,eq,3,and,parType2,eq,0,then
  *get,cmType,comp,%_cmaCap%,type
  *if,cmType,eq,8,and,_pileD,gt,0,then
    sliceCap = 1
  *endif
*endif

! Prepare environment
ksel , none
lsel , none
asel , none
cm   , cmk_support     , kp
cm   , cmk_pileTop     , kp
cm   , cml_pile        , line
cm   , cml_connections , line

*del,cap_cs,,nopr
*dim,cap_cs,,2

*get,pileCount,parm,%_pile_data%,dim,y

! Get pile cap CSYS
*if,_capOpt,eq,0,then
  ! Two connected pile caps:
  capCount  = 2
  cap_cs(1) = CS_INFRA_l%ARG1%, CS_INFRA_r%ARG1%
*elseif,_capOpt,eq,1,then
  ! Single pile cap:
  capCount  = 1
  cap_cs(1) = CS_INFRA_c%ARG1%
*endif

! Draw piles
*do,nthCap,1,capCount,1

  capCS = cap_cs(nthCap)
  csys,capCS

  *do,nthPile,1,pileCount,1
    ! Collect pile data
    xTop    = %_pile_data%(1,nthPile)
    yTop    = %_pile_data%(2,nthPile)
    pileLen = %_pile_data%(3,nthPile)
    yAngle  = %_pile_data%(4,nthPile)
    xAngle  = %_pile_data%(5,nthPile)

    ! Calculate pile's top & bottom (X,Y,Z) coordinates
    afunbackup,'DEG'
    zBot = zTop-pileLen
    xBot = xTop+tan(yAngle)*pileLen
    yBot = yTop+tan(xAngle)*pileLen
    afunrestore

    ! Create pile line
    lsel,none
    nextkp,'pile_ends',2
    k , pile_ends(1) , xTop , yTop , zTop
    k , pile_ends(2) , xBot , yBot , zBot
    l , pile_ends(1) , pile_ends(2)
    cmpadd,'cml_pile'

    ! Create connection line (if offset equal to pile cap thickness is applied)
    *if,zTop,ne,zZero,then
      lsel,none
      nextkp,'KP_connectionTop',1
      k , KP_connectionTop , xTop , yTop , zZero
      l, KP_connectionTop , pile_ends(1)
      cmpadd,'cml_connections'
    *endif

    ! Slice pile cap around pile
    *if,sliceCap,eq,1,then
      wpcsys,-1,capCS
      wpoffs,xTop,yTop
      BRIDGE_V1_SLICEAROUNDBEAM , _cmaCap , 'C' , _pileD , , , 'cma_cap%nthCap%_pile%nthPile%_itf'
    *endif

    ! Internal components
    ksel,s,kp,,pile_ends(1)
    cmpadd,'cmk_pileTop'
    ksel,s,kp,,pile_ends(2)
    cmpadd,'cmk_support'

  *enddo
*enddo

! Export & clean up
cmsel,s,cmk_support
cmdele , cmk_support
cm     , COMPK_%infix%support , kp

cmsel,s,cmk_pileTop
cmdele , cmk_pileTop
cm     , COMPK_%infix%pileTop , kp

cmsel,s,cml_pile
cmdele , cml_pile
cm     , COMPL_%infix%pile , line

cmsel,s,cml_connections
cmdele , cml_connections
cm     , COMPL_%infix%pileConnections , line

*if,sliceCap,eq,1,then
  *do,nthCap,1,capCount,1
    *do,nthPile,1,pileCount,1
      cmsel,s,cma_cap%nthCap%_pile%nthPile%_itf
      cm     , COMPA_SUP%_supIdx%_CAP%nthCap%_PILE%nthPile%_itf , area
      cmdele , cma_cap%nthCap%_pile%nthPile%_itf
    *enddo
  *enddo
*endif
