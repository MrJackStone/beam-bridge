! BRIDGE_V0_FEMMESO, nthSupport
! BRIDGE_V0_FEMMESO,       arg1


mesoType=GEO_mesoType(arg1)

*if,mesoType,eq,CTE_MESO_frame,then
    ! Column-girder frame:

    lcmName=strcat('COMPL_FRAME_',chrval(arg1))

    ! Activate coordinate system
    BRIDGE_V1_CSMESO, arg1

    ! Mesh frame girder
    esel,u,elem,,all
    nsel,u,node,,all
    cmsel,s,%lcmName%
    cmsel,r,COMPL_FrameGirder
    orient,TYPE_BEAM188,MAT_FrameGirder,SEC_FrameGirder_num,,,1,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_FrameGirder'
    cmpadd,'COMPN_FrameGirder'

    ! Mesh columns
    esel,u,elem,,all
    nsel,u,node,,all
    cmsel,s,%lcmName%
    cmsel,r,COMPL_Column
    orient,TYPE_BEAM188,MAT_Column,SEC_Column_num,-1,,,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_Column'
    cmpadd,'COMPN_Column'

    csys,0

  *elseif,mesoType,eq,CTE_MESO_beamcap,then
    ! Column-girder frame:

    lcmName=strcat('COMPL_FRAME_',chrval(arg1))

    ! Activate coordinate system
    BRIDGE_V1_CSMESO, arg1

    ! Mesh frame girder
    esel,u,elem,,all
    nsel,u,node,,all
    cmsel,s,%lcmName%
    cmsel,r,COMPL_FrameGirder
    orient,TYPE_BEAM188,MAT_FrameGirder,SEC_FrameGirder_num,,,1,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_FrameGirder'
    cmpadd,'COMPN_FrameGirder'

    ! Mesh columns using pile section
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    cmsel,r,COMPL_Column
    orient,TYPE_BEAM188,MAT_Column,SEC_Pile_num,-1,,,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_Column'
    cmpadd,'COMPN_Column'

    csys,0

*elseif,mesoType,eq,CTE_MESO_tube,or,mesoType,eq,CTE_MESO_tColumn,then
    ! Tubular pier column:

    acmName=strcat('COMPA_TUBE_',chrval(arg1))

    ! Walls
    esel,u,elem,,all
    nsel,u,node,,all
    cmsel,s,%acmName%
    cmsel,r,COMPA_coltubeWall
    aesize,all,FEM_PierESize
    aatt,MAT_Column,,TYPE_SHELL181,,SEC_COLWALL
    amesh,all
    cmpadd,'COMPE_ColTube'
    cmpadd,'COMPN_ColTube'

    ! Cap
    esel,u,elem,,all
    nsel,u,node,,all
    cmsel,s,%acmName%
    cmsel,r,COMPA_coltubeCap
    aesize,all,FEM_PierESize
    aatt,MAT_Column,,TYPE_SHELL181,,SEC_COLCAP
    amesh,all
    esla,s
    nsle,s,active
    cmpadd,'COMPN_colCap'
    cmpadd,'COMPE_colCap'

*endif


