! BRIDGE_V1_FEMMESO, nthSupport
! BRIDGE_V1_FEMMESO,       arg1


mesoType=GEO_mesoType(arg1)

*if,mesoType,eq,CTE_MESO_frame,then
    ! Column-girder frame:

    lcmName=strcat('COMPL_FRAME_',chrval(arg1))

    ! Activate coordinate system
    BRIDGE_V1_CSMESO, arg1

    ! Mesh frame girder
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    cmsel,r,COMPL_FrameGirder
    orient,TYPE_BEAM188,MAT_FrameGirder,SEC_FrameGirder_num,,,1,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_FrameGirder'
    cmpadd,'COMPN_FrameGirder'

    ! Mesh columns
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    cmsel,r,COMPL_Column
    orient,TYPE_BEAM188,MAT_Column,SEC_Column_num,1,,,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_Column'
    cmpadd,'COMPN_Column'

    csys,0

  *elseif,mesoType,eq,CTE_MESO_beamcap,then
    ! Column-girder frame:

    lcmName=strcat('COMPL_FRAME_',chrval(arg1))

    ! Activate coordinate system
    BRIDGE_V1_CSMESO, arg1

    ! Mesh frame girder
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    cmsel,r,COMPL_FrameGirder
    orient,TYPE_BEAM188,MAT_FrameGirder,SEC_FrameGirder_num,,,1,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_FrameGirder'
    cmpadd,'COMPN_FrameGirder'

    ! Mesh columns using pile section
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    cmsel,r,COMPL_Column
    orient,TYPE_BEAM188,MAT_Column,SEC_Pile_num,1,,,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_Column'
    cmpadd,'COMPN_Column'

    csys,0

*elseif,mesoType,eq,CTE_MESO_tube,or,mesoType,eq,CTE_MESO_tColumn,then
    ! Tubular pier column:

    acmName=strcat('COMPA_TUBE_',chrval(arg1))

    ! Walls
    esel,none
    nsel,none
    cmsel,s,%acmName%
    cmsel,r,COMPA_coltubeWall
    aesize,all,FEM_PierESize
    aatt,MAT_Column,,TYPE_SHELL181,,SEC_COLWALL
    amesh,all
    cmpadd,'COMPE_ColTube'
    cmpadd,'COMPN_ColTube'

    ! Cap
    esel,none
    nsel,none
    cmsel,s,%acmName%
    cmsel,r,COMPA_coltubeCap
    aesize,all,FEM_PierESize
    aatt,MAT_Column,,TYPE_SHELL181,,SEC_COLCAP
    amesh,all
    esla,s
    nsle,s,active
    cmpadd,'COMPN_colCap'
    cmpadd,'COMPE_colCap'

*elseif,mesoType,eq,CTE_MESO_hammerhead,then
    ! Hammerhead pier:

    lcmName = strcat('COMPL_HAMMER_',chrval(arg1))
    acmName = strcat('COMPA_HAMMER_',chrval(arg1))

    csbackup
    BRIDGE_V1_CSMESO, arg1

    ! Mesh column
    esel,none
    nsel,none
    cmsel,s,%lcmName%
    orient,TYPE_BEAM188,MAT_Column,SEC_COLUMN_num,1,,,FEM_ElementSize
    lmesh,all
    nsle,r,active
    cmpadd,'COMPE_Column'
    cmpadd,'COMPN_Column'

    ! Mesh pier cap
    esel,none
    nsel,none
    cmsel,s,%acmName%
    aesize,all,FEM_PierESize
    aatt,MAT_column,,TYPE_SHELL181,,SEC_colcap
    amesh,all
    nsle,s,active
    cm,COMPN_nthPiercap,node
    cmpadd,'COMPN_colCap'
    cmpadd,'COMPE_colCap'

    ! Couple column & cap with rigid region
    csnext,'CY_temp'
    clocal,CY_temp,cylin,0,0,0

    cmsel,s,COMPK_HAMMER_%ARG1%
    cmsel,r,COMPK_HammerColumnTop
    nslk,s
    ND_HAMMER%ARG1%_topMaster = ndnext(0)

    cmsel,s,COMPN_nthPiercap
    nsel,r,loc,x,0,GEO_pierColumnRadius
    nsel,a,node,,ND_HAMMER%ARG1%_topMaster

    cerig,ND_HAMMER%ARG1%_topMaster,all,all

    ! Clean up
    csrestore
    csdele,CY_temp

*endif
