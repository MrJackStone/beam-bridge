! BRIDGE_V1_POSTDECK

/post1
set,last

cmsel,s,compa_slab
esla,s
nsle,s,active
cm,vn,node
BRIDGE_V1_POSTSLABPATHS

*del,MPATHPLOT_ARR_pathNames
*dim,MPATHPLOT_ARR_pathNames,char,GEO_genSpanCount*5
*del,MPATHPLOT_ARR_labels
*dim,MPATHPLOT_ARR_labels,char,GEO_genSpanCount*5
*do,i,1,GEO_genSpanCount*5,1
  MPATHPLOT_ARR_pathNames(i)=pthNames(i)
  MPATHPLOT_ARR_labels(i)='ENEG_MX'
*enddo

allsel
esel,u,type,,3
cmsel,u,COMPE_pile
nsle,s,active

view1w
windefa,1
wincln,1
wntitle,'LAJES: MOM. FLETOR (-) [kNm/m]'

/view,1,-1,-1,1
/angle,1,0
/auto,1
eplot
/user,1

cmsel,a,COMPE_pile
cmsel,s,compa_slab
esla,u

/plopts,leg3,on
/udoc,1,cntr,right
/gformat,F,1,1
/edge,1,1

! Bending moments on superstructure slabs
go2png,1200
  eplot
  /noerase
  cmsel,s,compa_slab
  esla,s
  nsle,s,active
  /edge,1,0
  clims
  cleanplot,'pletab','ENEG_MX','1'
endpng,'IMG02a_slabMx_neg',,,,,memoImgsFolder
/erase
/annot,dele
clims

!!
!!
*do,i,1,GEO_genSpanCount*5,1
  MPATHPLOT_ARR_pathNames(i)=pthNames(i)
  MPATHPLOT_ARR_labels(i)='EPOS_MX'
*enddo

nsel,none
esel,none
*do,nthSupport,1,GEO_supportCount,1
  xSupport=GEO_supportX(nthSupport)
  nsel,a,loc,x,xSupport-0.5,xSupport+0.5
  esln,a
*enddo
cmsel,r,COMPE_slab
cmsel,r,COMPN_slab
cm,COMPE_slabIgnore,elem
cm,COMPN_slabIgnore,node

allsel
esel,u,type,,3
cmsel,u,COMPE_pile
nsle,s,active

view1w
windefa,1
wincln,1
wntitle,'LAJES: MOM. FLETOR (+) [kNm/m]'

/view,1,-1,-1,1
/angle,1,0
/auto,1
eplot
/user,1

cmsel,a,COMPE_pile
cmsel,s,compa_slab
esla,u
cmsel,a,COMPE_slabIgnore

/plopts,leg3,on
/udoc,1,cntr,right
/gformat,F,1,1
/edge,1,1

! Bending moments on superstructure slabs
go2png,1200
  eplot
  /noerase
  cmsel,s,compa_slab
  esla,s
  cmsel,u,COMPE_slabIgnore
  nsle,s,active
  /edge,1,0
  clims
  cleanplot,'pletab','EPOS_MX','1'
endpng,'IMG02b_slabMx_pos',,,,,memoImgsFolder
/erase
clims

!!
!!
*do,i,1,GEO_genSpanCount*5,1
  MPATHPLOT_ARR_pathNames(i)=pthNames(i)
  MPATHPLOT_ARR_labels(i)='EAB_Q'
*enddo

nsel,none
esel,none
*do,nthGirder,1,GEO_girderCount,1
  girderCS=CS_girders(nthGirder)
  csys,girderCS
  nsel,a,loc,y,-(SLAB_t+GIRDER_bfs0/2),(SLAB_t+GIRDER_bfs0/2)
  esln,a
  csys,0
*enddo
cmsel,r,COMPE_slab
cmsel,r,COMPN_slab
cmsel,a,COMPE_slabIgnore
cmsel,a,COMPN_slabIgnore
cm,COMPE_slabIgnore,elem
cm,COMPN_slabIgnore,node

allsel
esel,u,type,,3
cmsel,u,COMPE_pile
nsle,s,active

view1w
windefa,1
wincln,1
wntitle,'LAJES: FOR. CORTANTE [kN/m]'

/view,1,-1,-1,1
/angle,1,0
/auto,1
eplot
/user,1

cmsel,a,COMPE_pile
cmsel,s,compa_slab
esla,u
cmsel,a,COMPE_slabIgnore

/plopts,leg3,on
/udoc,1,cntr,right
/gformat,F,1,1
/edge,1,1

! Shear force on superstructure slabs
go2png,1200
  eplot
  /noerase
  cmsel,s,compa_slab
  esla,s
  cmsel,u,COMPE_slabIgnore
  nsle,s,active
  ! nsel,u,loc,x,0,0.5
  ! nsel,u,loc,x,GEO_deckLength,GEO_deckLength-0.5
  /edge,1,0
  clims
  cleanplot,'pletab','EAB_Q','1'
endpng,'IMG02c_slabQ',,,,,memoImgsFolder
/erase
clims


!!                                                                                                !!
!! Slab deflections                                                                               !!
!!                                                                                                !!
view1w
windefa,1
/device,font,3,Arial,1000,0,-64,0,0,,,
/device,font,1,Arial,1000,0,-42,0,0,,,
/device,text,1,100
/tspec,15,1.5,4,0,0
/pspec,0,0,1
/annot,dele
wincln,1
/udoc,1,cntr,right
/udoc,2,cntr,right
/plopts,leg3,on
/gformat,f,10,2
/annot,dele

!! Create element/node selection components for each 'span'                                       !!
*do,nthSpan,1,GEO_girderCount-1,1
    csA=CS_girders(nthSpan)
    csB=CS_girders(nthSpan+1)
    cmsel,s,COMPE_Slab
    nsle,s,active

    csys,csA
    nsel,r,loc,y,0,1e9

    csys,csB
    nsel,r,loc,y,-1e9,0

    esln,r,1
    cm,COMPE_SlabSpan_S%nthSpan%,elem
    cm,COMPN_SlabSpan_S%nthSpan%,node
*enddo

! Right overhang
cmsel,s,COMPE_Slab
nsle,s,active

csys,CS_rightDeckEdge
nsel,r,loc,y,0,1e9

csys,CS_girders(1)
nsel,r,loc,y,-1e9,0

esln,r,1
cm,COMPE_SlabSpan_SR,elem
cm,COMPN_SlabSpan_SR,node

! Left overhang
cmsel,s,COMPE_Slab
nsle,s,active

csys,CS_leftDeckEdge
nsel,r,loc,y,0,-1e9

csys,CS_girders(GEO_girderCount)
nsel,r,loc,y,0,1e9

esln,r,1
cm,COMPE_SlabSpan_SL,elem
cm,COMPN_SlabSpan_SL,node

!! Create paths                                                                                   !!
pathCount=50
cmsel,s,COMPE_Slab
nsle,s,active
*get,xmin,node,,mnloc,x
*get,xmax,node,,mxloc,x
xstep=(xmax-xmin)/(pathCount-1)
*del,x_path
*dim,x_path,,pathCount
*vfill,x_path,ramp,xmin,xstep

totalPathCount=(GEO_girderCount+1)*pathCount
*del,all_path_names
*dim,all_path_names,char,totalPathCount
*del,all_path_labels
*dim,all_path_labels,char,totalPathCount
pathIdx=0

*do,nthSpan,1,GEO_girderCount-1,1
    cmsel,s,COMPE_SlabSpan_S%nthSpan%
    cmsel,s,COMPN_SlabSpan_S%nthSpan%

    csA=CS_girders(nthSpan)
    *get,csAy,cdsy,csA,loc,y
    *get,csAxy,cdsy,csA,ang,xy

    csB=CS_girders(nthSpan+1)
    *get,csBy,cdsy,csB,loc,y
    *get,csBxy,cdsy,csB,ang,xy


    *do,nthPath,1,pathCount,1
        x=x_path(nthPath)
        *afun,deg
        yA=csAy+x*tan(csAxy)
        yB=csBy+x*tan(csBxy)
        *afun,rad

        ! Determine on which span the section lies
        *do,mthSpan,1,GEO_spanCount,1
          xi=GEO_supportX(mthSpan)
          xj=GEO_supportX(mthSpan+1)
          *if,x,ge,xi,and,x,le,xj,then
            activeSpan=mthSpan
            *exit
          *endif
        *enddo

        ! Calculate Z coordinate
        zi=zFrameTop+GEO_frameTopOffset(activeSpan)
        zj=zFrameTop+GEO_frameTopOffset(activeSpan+1)
        spanLength=GEO_spanlengths(activeSpan)
        zCoord=(zj-zi)*((x-xi)/spanLength)+zi

        pthName='S%nthSpan%_P%nthPath%'
        pathIdx=pathIdx+1
        all_path_names(pathIdx)=pthName
        all_path_labels(pathIdx)='NET_UZ'

        path,pthName,2,30,20
        ppath,1,,x,yA,zCoord
        ppath,2,,x,yB,zCoord
        pdef,'UZ',u,z,1
    *enddo
*enddo

! Right overhang paths
cmsel,s,COMPE_SlabSpan_SR
cmsel,s,COMPN_SlabSpan_SR

csA=CS_rightDeckEdge
*get,csAy,cdsy,csA,loc,y
*get,csAxy,cdsy,csA,ang,xy

csB=CS_girders(1)
*get,csBy,cdsy,csB,loc,y
*get,csBxy,cdsy,csB,ang,xy

*do,nthPath,1,pathCount,1
  x=x_path(nthPath)
  *afun,deg
  yA=csAy+x*tan(csAxy)
  yB=csBy+x*tan(csBxy)
  *afun,rad

  ! Determine on which span the section lies
  *do,nthSpan,1,GEO_spanCount,1
    xi=GEO_supportX(nthSpan)
    xj=GEO_supportX(nthSpan+1)
    *if,x,ge,xi,and,x,le,xj,then
      activeSpan=nthSpan
      *exit
    *endif
  *enddo

  ! Calculate Z coordinate
  zi=zFrameTop+GEO_frameTopOffset(activeSpan)
  zj=zFrameTop+GEO_frameTopOffset(activeSpan+1)
  spanLength=GEO_spanlengths(activeSpan)
  zCoord=(zj-zi)*((x-xi)/spanLength)+zi

  pthName='SR_P%nthPath%'
  pathIdx=pathIdx+1
  all_path_names(pathIdx)=pthName
  all_path_labels(pathIdx)='NET_UZ'

  path,pthName,2,30,20
  ppath,1,,x,yA,zCoord
  ppath,2,,x,yB,zCoord
  pdef,'UZ',u,z,1
*enddo

! Left overhang paths
cmsel,s,COMPE_SlabSpan_SL
cmsel,s,COMPN_SlabSpan_SL

csA=CS_girders(GEO_girderCount)
*get,csAy,cdsy,csA,loc,y
*get,csAxy,cdsy,csA,ang,xy

csB=CS_leftDeckEdge
*get,csBy,cdsy,csB,loc,y
*get,csBxy,cdsy,csB,ang,xy

*do,nthPath,1,pathCount,1
  x=x_path(nthPath)
  *afun,deg
  yA=csAy+x*tan(csAxy)
  yB=csBy+x*tan(csBxy)
  *afun,rad

  ! Determine on which span the section lies
  *do,nthSpan,1,GEO_spanCount,1
    xi=GEO_supportX(nthSpan)
    xj=GEO_supportX(nthSpan+1)
    *if,x,ge,xi,and,x,le,xj,then
      activeSpan=nthSpan
      *exit
    *endif
  *enddo

  ! Calculate Z coordinate
  zi=zFrameTop+GEO_frameTopOffset(activeSpan)
  zj=zFrameTop+GEO_frameTopOffset(activeSpan+1)
  spanLength=GEO_spanlengths(activeSpan)
  zCoord=(zj-zi)*((x-xi)/spanLength)+zi

  pthName='SL_P%nthPath%'
  pathIdx=pathIdx+1
  all_path_names(pathIdx)=pthName
  all_path_labels(pathIdx)='NET_UZ'

  path,pthName,2,30,20
  ppath,1,,x,yA,zCoord
  ppath,2,,x,yB,zCoord
  pdef,'UZ',u,z,1
*enddo

!! Calculate path envelopes                                                                       !!
*del,slab_envelope,,nopr
*dim,slab_envelope,char,2,totalPathCount
*do,nthPath,1,totalPathCount,1
  slab_envelope(1,nthPath)=all_path_names(nthPath)
  slab_envelope(2,nthPath)=all_path_labels(nthPath)
*enddo

pthenv,'CLEAR','slab_envelope'

*do,nthLS,1,LoadStepCount,1

  cmsel,s,COMPE_Slab
  nsle,s,active
  subset,nthLS,last

  ! Inner spans
  *do,nthSpan,1,GEO_girderCount-1,1
    cmsel,s,COMPE_SlabSpan_S%nthSpan%
    cmsel,s,COMPN_SlabSpan_S%nthSpan%
    *do,nthPath,1,pathCount,1
      pthName='S%nthSpan%_P%nthPath%'
      path,pthName
      pdef,'UZ',u,z,1
      pdef,'DW_UZ',etab,'DW_UZ',1
      pthrebase , 'UZ'    , 0 , 'NET_UZ'   , 1e3/1.4
      pthrebase , 'DW_UZ' , 0 , 'NETDW_UZ' , 1e3/1.4
    *enddo
  *enddo

  ! Right overhang paths
  cmsel,s,COMPE_SlabSpan_SR
  cmsel,s,COMPN_SlabSpan_SR
  *do,nthPath,1,pathCount,1
    pthName='SR_P%nthPath%'
    path,pthName
    pdef,'UZ',u,z,1
    pdef,'DW_UZ',etab,'DW_UZ',1
    pthrebase , 'UZ'    , 2 , 'NET_UZ'   , 1e3/1.4
    pthrebase , 'DW_UZ' , 2 , 'NETDW_UZ' , 1e3/1.4
  *enddo

  ! Left overhang paths
  cmsel,s,COMPE_SlabSpan_SL
  cmsel,s,COMPN_SlabSpan_SL
  *do,nthPath,1,pathCount,1
    pthName='SL_P%nthPath%'
    path,pthName
    pdef,'UZ',u,z,1
    pdef,'DW_UZ',etab,'DW_UZ',1
    pthrebase , 'UZ'    , 1 , 'NET_UZ'   , 1e3/1.4
    pthrebase , 'DW_UZ' , 1 , 'NETDW_UZ' , 1e3/1.4
  *enddo

  pthenv,'UPDATE','slab_envelope'
*enddo

!! Plot path items                                                                                !!
*del,max_envelope_labels,,nopr
*dim,max_envelope_labels,char,totalPathCount
*del,min_envelope_labels,,nopr
*dim,min_envelope_labels,char,totalPathCount
*del,dw_envelope_labels,,nopr
*dim,dw_envelope_labels,char,totalPathCount
*do,nthPath,1,totalPathCount,1
  max_envelope_labels(nthPath) = 'NET_UZ+'
  min_envelope_labels(nthPath) = 'NET_UZ-'
  dw_envelope_labels(nthPath)  = 'NETDW_UZ'
*enddo

*del,deltaZ,,nopr
z1=zFrameTop+GEO_frameTopOffset(1)
z2=zFrameTop+GEO_frameTopOffset(GEO_supportCount)
deltaZ=z2-z1
deckSlopeAngle=atan(deltaZ/GEO_deckLength)
xView=cos(deckSlopeAngle)
zView=sin(deckSlopeAngle)

! Deflection (RARA)
cmsel,s,COMPE_Slab
nsle,s,active
/window,all,dele
/window,1,-1,1.67,0,1
/window,2,-1,1.67,-1,0
wincln,1
wincln,2
/plopts,leg3,on
/gformat,f,10,2
/udoc,1,cntr,right
/udoc,2,cntr,right
/auto,all
/angle,all,0
/view,1,xView,0,zView
/view,2,xView,0,zView
/eshape,0
/edge,all,1

/annot  , dele
wintitle,1,'LAJE DO TABULEIRO','FLECHA MAX.','ELS-Rara [mm]',,1,13
wintitle,2,'LAJE DO TABULEIRO','FLECHA MIN.','ELS-Rara [mm]',,1,13

/window,1,on
/window,2,on
cmsel,s,COMPE_Slab
cmsel,a,COMPE_girder
nsle,s,active
/eshape,1
/gline,all,-1

go2png,1200
  eplot
  /user,all
  /noerase
  cmsel,s,COMPE_Slab
  nsle,s,active
  /eshape,0

  /window,1,on
  /window,2,off
  cleanplot,'mpathplot','5',,'all_path_names','max_envelope_labels'

  /window,1,off
  /window,2,on
  cleanplot,'mpathplot','5',,'all_path_names','min_envelope_labels'

endpng,'IMG10a_slab_deflections',,,,,'memo_imgs'
/erase
/annot,dele
clims
view4w
/gline,all,0
view1w
