! BRIDGE_V1_LONGSLICE , cmName , smKind
! BRIDGE_V1_LONGSLICE , ARG1   , ARG2


! Parse inputs
booleanOper = 'asbw'
cmKind      = 'area'

rebuildComponent = 0
*get,parType,parm,ARG1,type
*if,parType,eq,3,then
  rebuildComponent = 1
  cmsel,s,%ARG1%
  *get,cmType,comp,%ARG1%,type
  *if,cmType,eq,7,then
    booleanOper = 'lsbw'
    cmKind      = 'line'
  *elseif,cmType,eq,8,then
    booleanOper = 'asbw'
    cmKind      = 'area'
  *else
    *msg,error,ARG1,cmType
BRIDGE_V1_LONGSLICE error: selection component '%S' is of invalid type %I.%/&
BRIDGE_V1_LONGSLICE command ignored.
    *return,-1
  *endif
*endif

*if,rebuildComponent,eq,0,then
  *get,parType,parm,ARG2,type
  *if,parType,eq,3,then
    *if,upcase(ARG2),eq,'LINE',then
      booleanOper = 'lsbw'
      cmKind      = 'line'
    *endif
  *endif
*endif

*get,count,%cmKind%,,count
*if,count,eq,0,then
  *msg,warn,ARG1
BRIDGE_V1_LONGSLICE warning: %s selection component is empty.%/&
BRIDGE_V1_LONGSLICE command ignored.
  *return,-1
*endif

! Prepare environment
csbackup

! Slice solid model
*do,nthGirder,1,GEO_girderCount,1
  nthCS=CS_girders(nthGirder)
  csys,nthCS
  wpcsys,-1,nthCS
  wprota,,90
  %booleanOper%,all,,delete
  csys,0
  wpcsys,-1,0
*enddo

*do,nthLT,1,LOAD_ltWheelPathCount,1
  nthCS=CS_loadTrains(nthLT)
  csys,nthCS
  wpcsys,-1,nthCS
  wprota,,90
  %booleanOper%,all,,delete
  csys,0
  wpcsys,-1,0
*enddo

*if,LOAD_barrierStatus,eq,1,then
  *do,nthBarrier,1,LOAD_barrierCount,1
    nthCS=CS_barriers(nthBarrier)
    csys,nthCS
    wpcsys,-1,nthCS
    wprota,,90
    %booleanOper%,all,,delete
    csys,0
    wpcsys,-1,0
  *enddo
*endif

*if,GEO_footpathLeft_beg,gt,0,or,GEO_footpathLeft_end,gt,0,then
  csys,CS_leftFootpath
  wpcsys,-1
  wprota,,90
  %booleanOper%,all,,delete
  csys,0
  wpcsys,-1,0
*endif

*if,GEO_footpathRight_beg,gt,0,or,GEO_footpathRight_end,gt,0,then
  csys,CS_rightFootpath
  wpcsys,-1
  wprota,,90
  %booleanOper%,all,,delete
  csys,0
  wpcsys,-1,0
*endif

! Exported selection components
*if,rebuildComponent,eq,1,then
  cm,%ARG1%,%cmKind%
*endif

! Clean up
csrestore
