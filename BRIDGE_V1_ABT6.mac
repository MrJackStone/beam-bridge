! BRIDGE_V1_ABT6 , CS_local , geoArray
! BRIDGE_V1_ABT  , ARG1     , ARG2


! COMPA_Abutment
!
! COMPA_ABT_appSlab
!
! COMPL_ABT_piles
!
! COMPK_ABT_base
! COMPK_ABT_pileTop
! COMPK_ABT_pileBase


! Prepare environment
abackup
lbackup
kbackup
allsel
cm,COMPA_prev,area
cm,COMPL_prev,line
cm,COMPK_prev,kp
arestore
lrestore
krestore

csys,arg1

! Collect geometric parameters
GEO_ABT_appSlabL   = %arg2%(6)
GEO_ABT_height     = %arg2%(7)
GEO_ABT_length     = %arg2%(8)
GEO_ABT_pileL      = %arg2%(12)
GEO_ABT_topOffset  = %arg2%(13)
GEO_ABT_tranPiles  = %arg2%(14)
GEO_ABT_pileAngle  = %arg2%(18)
GEO_ABT_width      = %arg2%(28)

! Draw approach slab areas
*get,KMXD,kp,,num,maxd

KA  = KMXD+1
KB  = KMXD+2
KC  = KMXD+3
KD  = KMXD+4
x01 = 0
x05 = GEO_ABT_length
xc  = x01+GEO_ABT_appSlabL
y01 = -(GEO_ABT_width/2)
y02 = y01+GEO_ABT_width
z01 = 0
z03 = z01-GEO_ABT_height
zc  = z01+GEO_ABT_topOffset*(GEO_ABT_appSlabL/GEO_ABT_length)

asel,none
k , KA , x01 , y01 , z01
k , KB , x01 , y02 , z01
k , KC , xc  , y01 , zc
k , KD , xc  , y02 , zc

a, KA, KC, KD, KB

cmpadd,'COMPA_ABT_appSlab'

! Slice approach slab at bridge superstructure
cmsel,s,COMPA_ABT_appSlab
cmsel,u,COMPA_prev
BRIDGE_V1_LONGSLICE
cmpadd,'COMPA_ABT_appSlab'


! Calculate pile positions
deltaZT     = (GEO_ABT_width-2*GEO_ABT_pileOff)/(GEO_ABT_tranPiles-1)
firstZT     = GEO_ABT_pileOff
bot_delta_X = -GEO_ABT_pileL*tan(GEO_ABT_pileAngle*pi/180)

! Keypoints at pile cap level
cmsel,s,COMPK_ABT_temp ! FIXME
ksel,r,loc,z,z03
cmpadd,'COMPK_ABT_base'

! Keypoints at the bottom of the transversal wall
ksel,s,loc,x,x01
ksel,a,loc,x,x05
cmsel,r,COMPK_ABT_base

! Keypoints at the top of each transversal pile
ksel,none
y_i = y02-firstZT
*do,nthPile,1,GEO_ABT_tranPiles,1
  ksel,a,loc,y,y_i
  y_i=y_i-deltaZT
*enddo
cmsel,r,COMPK_ABT_base
cm,COMPK_ABT_temp1,kp

kgen,2,COMPK_ABT_temp1
cmsel,u,COMPK_ABT_temp1
cm,COMPK_twPileTop,kp
cmpadd,'COMPK_ABT_pileTop'

! Draw transversal wall pile lines
cmsel,s,COMPK_twPileTop
cmsel,u,COMPK_prev
lsel,none
*get,kmnx,kp,,mnloc,x
*get,kmxx,kp,,mxloc,x
*get,kc,kp,,count
knum = 0
*do,nthKP,1,kc,1
  cmsel,s,COMPK_twPileTop
  cmsel,u,COMPK_prev
  knum = kpnext(knum)
  ksel,s,kp,,knum
  nthK_deltaXmn = abs(kx(knum)-kmnx)
  nthK_deltaXmx = abs(kx(knum)-kmxx)
  fuzz          = 1e-3
  *if,nthK_deltaXmn,le,fuzz,then
    kgen,2,knum,,,bot_delta_X,,-GEO_ABT_pileL
  *elseif,nthK_deltaXmx,lt,fuzz,then
    kgen,2,knum,,,-bot_delta_X,,-GEO_ABT_pileL
  *else
    kgen,2,knum,,,0,,-GEO_ABT_pileL
  *endif
  *get,knew,kp,,num,max
  l,knum,knew
  ksel,s,kp,,knew
  cmpadd,'COMPK_ABT_pileBase'
*enddo
cmpadd,'COMPL_ABT_twPiles'
cmpadd,'COMPL_ABT_piles'

! Barrier lines selection component
lsel,none
*if,LOAD_barrierStatus,EQ,1,THEN
  *do,nthBarrier,1,LOAD_barrierCount,1
    nthCS=CS_barriers(nthBarrier)
    csys,nthCS
    lsel,a,loc,y,0
    csys,arg1
  *enddo
  cmsel,s,COMPA_ABT_appSlab
  lsla,r
*endif
cmpadd,'COMPL_Barrier'

! Whole abutment selection components
asel,none
cmsel,a,COMPA_ABT_appSlab
cmpadd,'COMPA_Abutment'

lsla,s
cmsel,a,COMPL_ABT_piles
cmpadd,'COMPL_Abutment'

ksll,s
cmpadd,'COMPK_Abutment'

allsel
cmsel,u,COMPA_prev
cmsel,u,COMPL_prev
cmsel,u,COMPK_prev

! Coordinate systems for ESYS
csys,arg1
csnext,'CS_leftWall'
clocal,CS_leftWall,cart,0,0,0,,90  ! TODO  csys
csnext,'CS_rightWall'
clocal,CS_leftWall,cart,0,0,0,180,90  ! TODO  csys
