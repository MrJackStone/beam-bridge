! BRIDGE_V1_SMTANDECK

! Deck area
*get,kmaxD,kp,,num,maxd
k1=kmaxD+1
k2=kmaxD+2
k3=kmaxD+3
k4=kmaxD+4
k,k1,0             ,GEO_deckOff_beg                  ,0
k,k2,0             ,GEO_deckOff_beg+GEO_deckWidth_beg,0
k,k3,GEO_deckLength,GEO_deckOff_end                  ,0 !! TODO: Z needs to be different from 0 for sloped decks
k,k4,GEO_deckLength,GEO_deckOff_end+GEO_deckWidth_end,0
a,k1,k2,k4,k3

! Cut spans
*do,nthSupport,1,GEO_supportCount,1
    allsel
    xCoord=GEO_supportX(nthSupport)
    zCoord=GEO_frameTopOffset(nthSupport)
    *if,nthSupport,gt,1,and,nthSupport,lt,GEO_supportCount,then
      wpsliceyz,xCoord,1
    *endif
    *if,zCoord,ne,0,then
        ksel,r,loc,x,xCoord
        kmodif,all,,,zCoord
    *endif
*enddo

! Cut at midspans
allsel
batch_sliceyz,'GEO_midspanX'

! Cut at gerber connections
*if,GEO_gerberStatus,eq,1,then
    batch_sliceyz,'GEO_gerberX_i',,1
    batch_sliceyz,'GEO_gerberX_j',,1
*endif

! Cut at tubular column faces
*if,GEO_pierCount,gt,0,and,GEO_frameOpt,eq,1,then
    batch_sliceyz,'GEO_pierX_i'
    batch_sliceyz,'GEO_pierX_j'
*endif

! Perform longitudinal cuts on the deck (footpaths, girders, load trains)
BRIDGE_V1_LONGSLICE

!! Selection components                                                                           !!
! Create component with girder lines & keypoints
lsel,u,line,,all
*do,nthGirder,1,GEO_girderCount,1
    girderCS=CS_girders(nthGirder)
    girderCompName=strcat('COMPL_GIRDER_',chrval(nthGirder))
    csys,girderCS
    lsel,s,loc,y,0
    cmpadd,girderCompName
    cmpadd,'COMPL_Girder'
    csys,0
*enddo
cmsel,s,COMPL_Girder
ksll,s
cm,COMPK_Girder,kp

! Create component with gerber beams
lsel,u,line,,all
ksel,u,kp,,all
*if,GEO_gerberStatus,eq,1,then
    *do,nthGerber,1,GEO_gerberCount,1
        x_i=GEO_gerberX_i(nthGerber)
        x_j=GEO_gerberX_j(nthGerber)
        lsel,a,loc,x,x_i,x_j
    *enddo
    cmsel,r,COMPL_Girder
    ksll,s
    cm,COMPL_Gerber,line
    cm,COMPK_Gerber,kp
    cmsel,s,COMPL_Girder
    cmsel,u,COMPL_Gerber
    cm,COMPL_Girder,line
    ksll,s
    cm,COMPK_Girder,kp
*else
    cm,COMPL_Gerber,line
    cm,COMPK_Gerber,kp
*endif
*do,nthGirder,1,GEO_girderCount,1
    girderCompName=strcat('COMPL_GIRDER_',chrval(nthGirder))
    lcmName=strcat('COMPL_GERBER_',chrval(nthGirder))
    cmsel,s,COMPL_Gerber
    cmsel,r,%girderCompName%
    cm,%lcmName%,line
*enddo

! Create component with load train path lines
lsel,u,line,,all
*do,nthLT,1,LOAD_wheelCount,1
    ltCS=CS_loadTrains(nthLT)
    csys,ltCS
    lsel,a,loc,y,0
    csys,0
*enddo
ksll,s
cm,COMPL_LoadTrain,line
cm,COMPK_LoadTrain,kp

! Create component with barrier lines
lsel,u,line,,all
cm,COMPL_Barrier,line
*if,LOAD_barrierStatus,EQ,1,THEN
    *do,nthBarrier,1,LOAD_barrierCount,1
        barrierCS=CS_barriers(nthBarrier)
        csys,barrierCS
        lsel,a,loc,y,0
    *enddo
    cm,COMPL_Barrier,line
*endif
csys,0

! Create component with support keypoints
ksel,u,kp,,all
ksel,a,loc,x,GEO_supportX(1:GEO_supportCount)
*if,GEO_gerberStatus,eq,1,then
    ksel,a,loc,x,GEO_gerberX_i(1:GEO_gerberCount)
    ksel,a,loc,x,GEO_gerberX_j(1:GEO_gerberCount)
*endif
cmsel,r,COMPK_Girder
cm,COMPK_GirderSupport,kp

! Create gerber consoles
lsel,u,line,,all
*if,GEO_gerberStatus,eq,1,then
    *do,nthGerber,1,GEO_gerberCount,1
        x_tip=GEO_gerberX_i(nthGerber)
        *do,x_idx,1,2,1
            *do,nthConsole,1,GEO_girderCount-1,1
                *if,nthConsole,eq,1,then
                    lcmName_1=strcat('COMPL_GERBER_',chrval(nthConsole))
                    cmsel,s,%lcmName_1%
                    ksll,s
                    ksel,r,loc,x,x_tip
                    k_A=kpnext(0)
                *else
                    k_A=k_B
                *endif

                lcmName_2=strcat('COMPL_GERBER_',chrval(nthConsole+1))
                cmsel,s,%lcmName_2%
                ksll,s
                ksel,r,loc,x,x_tip
                k_B=kpnext(0)

                ksel,s,kp,,k_A
                ksel,a,kp,,k_B
                lsel,u,line,,all
                l,k_A,k_B
                cmpadd,'COMPL_Console'
            *enddo
            x_tip=GEO_gerberX_j(nthGerber)
        *enddo
    *enddo
*endif
ksll,s
cm,COMPL_GerberConsole,line
cm,COMPK_GerberConsole,kp

! Create component with gerber support keypoints
ksel,u,kp,,all
cm,COMPK_GerberTip,kp
cm,COMPK_GerberSupported,kp
*if,GEO_gerberStatus,eq,1,then
    ! Gerber tip (cantilever-girder connection)
    ksel,u,kp,,all
    *do,nthGerber,1,GEO_gerberCount,1
        x_i=GEO_gerberX_i(nthGerber)
        x_j=GEO_gerberX_j(nthGerber)
        ksel,a,loc,x,x_i
        ksel,a,loc,x,x_j
    *enddo
    cmsel,r,COMPK_Gerber
    cm,COMPK_GerberTip,kp
    ! Centilever support (over tubular column)
    ksel,u,kp,,all
    *do,nthGerber,1,GEO_gerberCount,1
        x_m=GEO_gerberX_m(nthGerber)
        x_n=GEO_gerberX_n(nthGerber)
        ksel,a,loc,x,x_m,x_n
    *enddo
    cmsel,r,COMPK_Gerber
    cm,COMPK_GerberSupported,kp
*endif

! Create component with left pedestrian footpath areas
*if,GEO_footpathLeft,GT,0,THEN
    csys,CS_leftFootpath
    asel,s,loc,y,0,(GEO_footpathLeft_beg>GEO_footpathLeft_end)
    cm,COMPA_LeftFootpath,area
    csys,0
*else
    asel,u,area,,all
    cm,COMPA_LeftFootpath,area
*endif

! Create component with right pedestrian footpath areas
*if,GEO_footpathRight,GT,0,THEN
    csys,CS_rightFootpath
    asel,s,loc,y,-(GEO_footpathRight_beg>GEO_footpathRight_end),0
    cm,COMPA_RightFootpath,area
    csys,0
*else
    asel,u,area,,all
    cm,COMPA_RightFootPath,area
*endif

