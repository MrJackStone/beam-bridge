! BRIDGE_V1_ABT4, CS_local, geoArray
! BRIDGE_V1_ABT4,     arg1,     arg2

!!                                                                                                !!
!! Documentation:                                                                                 !!
!!                                                                                                !!
! Interface input objects:
!   SEC_ABT_pileCap_h
!   GEO_ABT_pileOff
!   BRIDGE_V1_LONGSLICE

! Interface output objects:
!   COMPA_ABT_appSlab
!   COMPA_ABT_longWalls
!   COMPA_ABT_pileCap
!   COMPA_ABT_tranWall
!   COMPA_ABT_thinTranWall
!   COMPA_ABT_walls
!   COMPA_Abutment
!   COMPK_ABT_base
!   COMPK_ABT_pileBase
!   COMPK_ABT_pileTop
!   COMPK_Abutment
!   COMPL_ABT_lwPiles
!   COMPL_ABT_pileConnections
!   COMPL_ABT_piles
!   COMPL_ABT_twPiles
!   COMPL_Abutment
!   COMPL_Barrier

!!                                                                                                !!
!! A. Abutment geometry                                                                           !!
!!                                                                                                !!
! Load geometric parameters
GEO_ABT_deckWidth     = %arg2%(2)
GEO_ABT_appSlabL      = %arg2%(6)
GEO_ABT_height        = %arg2%(7)
GEO_ABT_longPiles     = %arg2%(9)
GEO_ABT_pileCapL      = %arg2%(10)
GEO_ABT_pileCapW      = %arg2%(11)
GEO_ABT_pileL         = %arg2%(12)
GEO_ABT_tranPiles     = %arg2%(14)
GEO_ABT_wingHa        = %arg2%(16)
GEO_ABT_wingL         = %arg2%(17)
GEO_ABT_trvPileCols   = %arg2%(23)
GEO_ABT_wingPileCols  = %arg2%(24)
GEO_ABT_wingCapLength = %arg2%(25)
GEO_ABT_wingCapWidth  = %arg2%(26)
GEO_ABT_wingAngle     = %arg2%(27)
GEO_ABT_thinDepth     = %arg2%(29)

!!                                                                                                !!
!! B. Draw geometry                                                                               !!
!!                                                                                                !!
! Store previous selection
abackup
lbackup
kbackup
allsel
cm,COMPA_prev,area
cm,COMPL_prev,line
cm,COMPK_prev,kp
arestore
lrestore
krestore

!! B.1 Transversal wall and its foundation                                                        !!
csys,arg1
csnext,'CS_trvWall'
clocal,CS_trvWall,0,0,-GEO_ABT_deckWidth/2,-GEO_ABT_height
csys,CS_trvWall

*del,transversal_wall_params,,nopr
*dim,transversal_wall_params,,10
transversal_wall_params(1) = GEO_ABT_deckWidth , GEO_ABT_height , GEO_ABT_height    , GEO_ABT_pileCapL    , GEO_ABT_pileCapW
transversal_wall_params(6) = SEC_ABT_pileCap_h , GEO_ABT_pileL  , GEO_ABT_tranPiles , GEO_ABT_trvPileCols , GEO_ABT_pileOff

sm_pilesup_retwall,CS_trvWall,'transversal_wall_params', 'ABT_TRV'
csys,arg1

!! B.2 Left wing and its foundation                                                               !!
csys,arg1
csnext,'CS_leftWing'
clocal,CS_leftWing,0,0,GEO_ABT_deckWidth/2,-GEO_ABT_height,GEO_ABT_wingAngle-90
csys,CS_leftWing

*del,left_wing_params,,nopr
*dim,left_wing_params,,10
left_wing_params(1) = GEO_ABT_wingL     , GEO_ABT_height , GEO_ABT_wingHa    , GEO_ABT_wingCapLength , GEO_ABT_wingCapWidth
left_wing_params(6) = SEC_ABT_pileCap_h , GEO_ABT_pileL  , GEO_ABT_longPiles , GEO_ABT_wingPileCols  , GEO_ABT_pileOff

asel,none
sm_pilesup_retwall,CS_leftWing,'left_wing_params', 'ABT_LW'
cmpadd,'COMPA_ABT_leftSide'

! Delete first pile row (overlaps with transverwall wall's)
*do,nthPileCol,1,GEO_ABT_wingPileCols,1
  cmsel,s,COMPL_ABT_LW_pile%nthPileCol%
  cmsel,a,COMPL_ABT_LW_pileConnection%nthPileCol%
  ksll,s
  ldele,all,,,1
*enddo

csys,arg1

!! B.2 Right wing and its foundation                                                              !!
csys,arg1
csnext,'CS_rightWing'
clocal,CS_rightWing,0,0,-GEO_ABT_deckWidth/2,-GEO_ABT_height,-GEO_ABT_wingAngle-90
csys,CS_rightWing

*del,right_wing_params,,nopr
*dim,right_wing_params,,10
right_wing_params(1) = GEO_ABT_wingL     , GEO_ABT_height, GEO_ABT_wingHa    , GEO_ABT_wingCapLength , GEO_ABT_wingCapWidth
right_wing_params(6) = SEC_ABT_pileCap_h , GEO_ABT_pileL , GEO_ABT_longPiles , GEO_ABT_wingPileCols  , GEO_ABT_pileOff

asel,none
sm_pilesup_retwall,CS_rightWing,'right_wing_params', 'ABT_RW'
cmpadd,'COMPA_ABT_rightSide'

! Reverse direction of wall area, so it points inwards relative to the abutment
cmsel,s,COMPA_ABT_RW_wall
areverse,all

! Delete first pile row (overlaps with transverwall wall's)
*do,nthPileCol,1,GEO_ABT_wingPileCols,1
  cmsel,s,COMPL_ABT_RW_pile%nthPileCol%
  cmsel,a,COMPL_ABT_RW_pileConnection%nthPileCol%
  ksll,s
  ldele,all,,,1
*enddo

csys,arg1

!! B.3 Join walls                                                                                 !!
cmsel,s,COMPA_ABT_TRV_wall
cmsel,a,COMPA_ABT_LW_wall
cmsel,a,COMPA_ABT_RW_wall
lsla,s
ksll,s
nummrg,kp

!! B.4 Slice transversal wall at thickness change                                                 !!
*if,GEO_ABT_thinDepth,gt,0,then
  csys,CS_trvWall
  wpcsys,-1,CS_trvWall
  wpoffs,,,GEO_ABT_height-GEO_ABT_thinDepth

  cmsel,s,COMPA_ABT_TRV_wall
  asbw,all,,delete
  cm,COMPA_ABT_TRV_wall,area

  asel,r,loc,z,GEO_ABT_height-GEO_ABT_thinDepth,GEO_ABT_height
  cm,COMPA_ABT_TRV_thinWall,area

  csys,arg1

*else
  asel,none
  cm,COMPA_ABT_TRV_thinWall,area

*endif

!! B.5 Draw approach slab                                                                         !!
! Copy line pattern from transversal wall top
cmsel,s,COMPA_ABT_TRV_wall
lsla,s
lsel,r,loc,z,0
cm,COMPL_temp_trvWallTop,line
lgen,2,all,,,0,0,0,,,0
cmsel,u,COMPL_temp_trvWallTop
cm,COMPL_temp_abtInterface,line

! Gather/create keypoints at the approach slab's corners
cmsel,s,COMPL_temp_abtInterface
ksll,s
twLeftKP=kp(0,GEO_ABT_deckWidth/2,0)
twRightKP=kp(0,-GEO_ABT_deckWidth/2,0)
nextkp,'asLeftKP'
k,asLeftKP,GEO_ABT_appSlabL,GEO_ABT_deckWidth/2,0
nextkp,'asRightKP'
k,asRightKP,GEO_ABT_appSlabL,-GEO_ABT_deckWidth/2,0

! Draw new lines to close approach slab's perimeter
lsel,u,line,,all
l,twLeftKP,asLeftKP
l,twRightKP,asRightKP
l,asLeftKP,asRightKP
cm,COMPL_temp_appSlab,line

! Draw approach slab
cmsel,s,COMPL_temp_appSlab
cmsel,a,COMPL_temp_abtInterface
ksll,s
al,all
cm,COMPA_TEMP_appSlab,area

!!                                                                                                !!
!! C. Slice to connect to superstructure                                                          !!
!!                                                                                                !!
! Slice transversal wall and pile cap
cmsel,s,COMPA_ABT_TRV_wall
cmsel,a,COMPA_ABT_TRV_cap
lsla,s
ksll,s
BRIDGE_V1_LONGSLICE
nummrg,kp
cm,COMPA_ABT_temp,area
asel,r,loc,x,0
cm,COMPA_ABT_TRV_wall,area
asel,inve
cmsel,r,COMPA_ABT_temp
cm,COMPA_ABT_TRV_cap,area

! Slice approach slab
cmsel,s,COMPA_TEMP_appSlab
BRIDGE_V1_LONGSLICE
cm,COMPA_TEMP_appSlab,area

!!                                                                                                !!
!! D. Merge pile caps                                                                             !!
!!                                                                                                !!
cmsel,s,COMPA_ABT_TRV_cap
cmsel,a,COMPA_ABT_LW_cap
aovlap,all
cm,COMPA_leftSideComplement,area

cmsel,a,COMPA_ABT_RW_cap
aovlap,all
cm,COMPA_ABT_mergedCaps,area

! Collect left/right side cap areas to rebuild degenerated components later
cmsel,u,COMPA_leftSideComplement
cm,COMPA_rightSideComplement,area

cmsel,s,COMPA_leftSideComplement
cmsel,r,COMPA_rightSideComplement
cm,COMPA_intersection,area

cmsel,s,COMPA_leftSideComplement
cmsel,u,COMPA_intersection
cm,COMPA_leftSideComplement,area

cmsel,s,COMPA_rightSideComplement
cmsel,u,COMPA_intersection
cm,COMPA_rightSideComplement,area

!!                                                                                                !!
!! E. Interface output: create selection components                                               !!
!!                                                                                                !!
! Barrier lines
lsel,u,line,,all
*if,LOAD_barrierStatus,EQ,1,THEN
  *do,nthBarrier,1,LOAD_barrierCount,1
    nthCS=CS_barriers(nthBarrier)
    csys,nthCS
    lsel,a,loc,y,0
    csys,arg1
  *enddo
  cmsel,s,COMPA_ABT_appSlab
  lsla,r
*endif
cmpadd,'COMPL_Barrier'

! Sides
cmsel,s,COMPA_leftSideComplement
cmpadd,'COMPA_leftSide'
cmsel,s,COMPA_rightSideComplement
cmpadd,'COMPA_ABT_rightSide'

! Approach slab
cmsel,s,COMPA_TEMP_appSlab
cmpadd,'COMPA_ABT_appSlab'

! Abutment walls (transversal & wings)
cmsel,s,COMPA_ABT_LW_wall
cmsel,a,COMPA_ABT_RW_wall
cmpadd,'COMPA_ABT_longWalls'
cmsel,s,COMPA_ABT_TRV_wall
cmpadd,'COMPA_ABT_tranWall'
cmsel,s,COMPA_ABT_longWalls
cmsel,a,COMPA_ABT_tranWall
cmpadd,'COMPA_ABT_walls'
cmsel,s,COMPA_ABT_TRV_thinWall
cmpadd,'COMPA_ABT_thinTranWall'

! Pile caps
cmsel,s,COMPA_ABT_mergedCaps
cmpadd,'COMPA_ABT_pileCap'

cmsel,s,COMPK_ABT_TRV_ref
cmsel,a,COMPK_ABT_LW_ref
cmsel,a,COMPK_ABT_RW_ref
cmpadd,'COMPK_ABT_base'

! Piles
cmsel,s,COMPL_ABT_TRV_piles
cmpadd,'COMPL_ABT_twPiles'
cmsel,s,COMPL_ABT_LW_piles
cmsel,a,COMPL_ABT_RW_piles
cmpadd,'COMPL_ABT_lwPiles'
cmsel,s,COMPL_ABT_twPiles
cmsel,a,COMPL_ABT_lwPiles
cmpadd,'COMPL_ABT_piles'

! Pile connections
cmsel,s,COMPL_ABT_TRV_pileConnections
cmsel,a,COMPL_ABT_LW_pileConnections
cmsel,a,COMPL_ABT_RW_pileConnections
cmpadd,'COMPL_ABT_pileConnections'

! Pile ends
cmsel,s,COMPK_ABT_TRV_pileTop
cmsel,a,COMPK_ABT_LW_pileTop
cmsel,a,COMPK_ABT_RW_pileTop
cmpadd,'COMPK_ABT_pileTop'

cmsel,s,COMPK_ABT_TRV_pileBot
cmsel,a,COMPK_ABT_LW_pileBot
cmsel,a,COMPK_ABT_RW_pileBot
cmpadd,'COMPK_ABT_pileBase'

! Whole abutment
cmsel,s,COMPA_ABT_walls
cmsel,a,COMPA_ABT_pileCap
cmsel,a,COMPA_ABT_appSlab
cmpadd,'COMPA_Abutment'

lsla,s
cmsel,a,COMPL_ABT_piles
cmsel,a,COMPL_ABT_pileConnections
cmpadd,'COMPL_Abutment'

ksll,s
cmpadd,'COMPK_Abutment'

! Keep selected only newly created entities
allsel
cmsel,u,COMPA_prev
cmsel,u,COMPL_prev
cmsel,u,COMPK_prev
