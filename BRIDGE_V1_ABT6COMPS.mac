! BRIDGE_V1_ABT6COMPS , abtIdx , supIndex
! BRIDGE_V1_ABT6COMPS , ARG1   , ARG2

! Dependencies:
!   GEO_ABT_capOpt
!   GEO_ABT<i>_pile_data
!
!   cmk_postTops
!   cml_columns
!   cml_frameGirder
!   cml_posts
!   cma_approachSlab
!   cma_cantileverWall
!
!   COMPK_ABT<i>_support
!   COMPK_ABT<i>_pileTop
!   COMPL_ABT<i>_pileConnections
!   COMPL_ABT<i>_pile
!   COMPL_ABT<i>_gradeBeam
!   COMPA_ABT<i>_pileCap
!   COMPA_SUP<i>_CAP<j>_PILE<k>_itf
!
!   CS_barriers
!   LOAD_barrierStatus
!   LOAD_barrierCount

! Exported:
!   COMPK_ABT_postTops
!   COMPK_ABT_pileBase
!   COMPK_ABT_pileTop
!   COMPL_ABT_columns
!   COMPL_ABT_frameGirder
!   COMPL_ABT_gradeBeam
!   COMPL_ABT_pileConnections
!   COMPL_ABT_piles
!   COMPL_ABT_posts
!   COMPL_ABT_cantileverWallBottom
!   COMPL_Barrier
!   COMPA_ABT_appSlab
!   COMPA_ABT_cantileverWall
!   COMPA_ABT_pileCap
!   COMPA_ABT_pileInterface
!   COMPA_ABT<i>_CAP<j>_PILE<k>_itf



*if,GEO_ABT_capOpt,eq,0,then
  capCount = 2
*elseif,GEO_ABT_capOpt,eq,1,then
  capCount = 1
*endif

*get,pileCount,parm,GEO_ABT%ARG1%_pile_data,dim,y

cmsel,s,cma_approachSlab
cmpadd,'COMPA_ABT_appSlab'

cmsel,s,cma_cantileverWall
cmpadd,'COMPA_ABT_cantileverWall'

cmsel,s,cma_cantileverWall
lsla
ksll
*get,zMin,kp,,mnloc,z
lsel,r,loc,z,zMin
cmpadd,'COMPL_ABT_cantileverWallBottom'

cmsel,s,cml_columns
cmpadd,'COMPL_ABT_columns'

cmsel,s,cml_frameGirder
cmpadd,'COMPL_ABT_frameGirder'

cmsel,s,cml_posts
cmpadd,'COMPL_ABT_posts'

cmsel,s,cmk_postTops
cmpadd,'COMPK_ABT_postTops'

cmsel,s,COMPL_ABT%ARG1%_gradeBeam
cmpadd,'COMPL_ABT_gradeBeam'

cmsel,s,COMPK_ABT%ARG1%_support
cmpadd,'COMPK_ABT_pileBase'

cmsel,s,COMPK_ABT%ARG1%_pileTop
cmpadd,'COMPK_ABT_pileTop'

cmsel,s,COMPL_ABT%ARG1%_pileConnections
cmpadd,'COMPL_ABT_pileConnections'

cmsel,s,COMPL_ABT%ARG1%_pile
cmpadd,'COMPL_ABT_piles'

cmsel,s,COMPA_ABT%ARG1%_pileCap
cmpadd,'COMPA_ABT_pileCap'

*do,nthCap,1,capCount,1
  *do,nthPile,1,pileCount,1
    cmsel , s , COMPA_SUP%ARG2%_CAP%nthCap%_PILE%nthPile%_itf
    cm , COMPA_ABT%ARG1%_CAP%nthCap%_PILE%nthPile%_itf , area
    cmpadd,'COMPA_ABT_pileInterface'
  *enddo
*enddo

lsel,none
*if,LOAD_barrierStatus,EQ,1,THEN
  csbackup
  *do,nthBarrier,1,LOAD_barrierCount,1
    nthCS = CS_barriers(nthBarrier)
    csys,nthCS
    lsel,a,loc,y,0
  *enddo
  cmsel,s,COMPA_ABT_appSlab
  lsla,r
  csrestore
*endif
cmpadd,'COMPL_Barrier'

! Clean up
cmdele , cml_columns
cmdele , cml_frameGirder
cmdele , cml_posts
cmdele , cma_approachSlab
cmdele , cma_cantileverWall
