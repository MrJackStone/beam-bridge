! BRIDGE_V1_FEMBEARING , bearingType , girderNode , mesoNode
! BRIDGE_V1_FEMBEARING , ARG1        , ARG2       , ARG3


! Parse inputs
_bearingType = ARG1
_girderNode  = ARG2
_mesoNode    = ARG3

bearingStatus = valchr(_bearingType)
*if,bearingStatus,eq,0,then
  *msg,warn
BRIDGE_V1_FEMBEARING warning: inactive girder bearing between nodes I% and I%.%/&
This girder will not be connected to this support.%/&
Check input to make sure this is the intended behavior.%/&
BRIDGE_V1_FEMBEARING macro ignored.
  *return,-1
*endif

! Set constants
bitCount = 6

*del , bit_dofs ,      , nopr
*dim , bit_dofs , char , bitCount
bit_dofs(1) = 'UX' , 'UY' , 'UZ' , 'ROTX' , 'ROTY' , 'ROTZ'

*del , dof_rcs ,  , nopr
*dim , dof_rcs ,  , bitCount
dof_rcs(1) = REAL_bearingx , REAL_bearingy , REAL_bearingz , 0 , 0 , 0

! Prepare environment
nsel , s , node ,  , _mesoNode
nsel , a , node ,  , _girderNode

! Connect nodes
*do,nthBit,1,bitCount,1

  ! Connection settings
  bitValue = strsub(_bearingType,nthBit,1)
  bitDOF   = bit_dofs(nthBit)
  rcNum    = dof_rcs(nthBit)

  *if,bitValue,eq,'0',then
    ! No connection

  *elseif,bitValue,eq,'1',then
    ! Coupling
    cp,next,%bitDOF%,_mesoNode,_girderNode

  *elseif,bitValue,eq,'2',then
    ! Spring
    *if,rcNum,eq,0,cycle
    ! TODO  torsional springs for ROT DOFs not implemented
    type , TYPE_matrix27
    mat  , MAT_matrix
    real , rcNum
    e,_mesoNode,_girderNode

  *endif
*enddo
