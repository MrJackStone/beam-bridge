! BRIDGE_V1_POSTAPSLBDFL, abtNum
! BRIDGE_V1_POSTAPSLBDFL, arg1

!! Initial configs                                                                                !!
view1w
windefa,1
/device,font,3,Arial,1000,0,-64,0,0,,,
/device,font,1,Arial,1000,0,-42,0,0,,,
/device,text,1,100
/tspec,15,1.5,4,0,0
/gformat,f,10,2
/annot,dele

pathCount=10

cmsel,s,COMPA_ABT_appSlab
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active

csys,CS_ABT%arg1%
*get,ymin,node,,mnloc,y
*get,ymax,node,,mxloc,y
csys,0

yStep=(yMax-yMin)/(pathCount-1)
xLocal1=0
xLocal2=GEO_ABT%arg1%_appSlabL

*del , path_names  ,      , nopr
*dim , path_names  , char , pathCount
*del , path_labels ,      , nopr
*dim , path_labels , char , pathCount

!! Create paths                                                                                   !!
*do,nthPath,1,pathCount,1
  yLocal=yMin+yStep*(nthPath-1)
  loc2glo,xLocal1,yLocal,0,CS_ABT%arg1%,'x1g','y1g','z1g'
  loc2glo,xLocal2,yLocal,0,CS_ABT%arg1%,'x2g','y2g','z2g'

  pathName='APS_P%nthPath%'
  path_names(nthPath)=pathName
  path_labels(nthPath)='NET_UZ'

  path,pathName,2,30,50
  *if,arg1,eq,1,then
    ppath,1,,x1g,y1g,z1g
    ppath,2,,x2g,y2g,z2g
  *elseif,arg1,eq,2,then
    ppath,1,,x2g,y2g,z2g
    ppath,2,,x1g,y1g,z1g
  *endif
  pdef,'UZ',u,z,avg
*enddo

!! Calculate path envelopes                                                                       !!
*del,slab_envelope,,nopr
*dim,slab_envelope,char,2,pathCount
*do,nthPath,1,pathCount,1
  slab_envelope(1,nthPath)=path_names(nthPath)
  slab_envelope(2,nthPath)=path_labels(nthPath)
*enddo

pthenv,'CLEAR','slab_envelope'

cmsel,s,COMPA_ABT_appSlab
cmsel,r,COMPA_ABT_%arg1%
esla,s
nsle,s,active

*do,nthLS,1,LoadStepCount,1

  subset,nthLS,last

  *do,nthPath,1,pathCount,1
    pathName='APS_P%nthPath%'
    path,pathName
    pdef,'UZ',u,z,avg
    pthrebase,'UZ',0,'NET_UZ',1e3/1.4
  *enddo

  pthenv,'UPDATE','slab_envelope'
*enddo

!! Plot path envelopes                                                                            !!
*del , envelope_path_names  ,      , nopr
*dim , envelope_path_names  , char , pathCount*2
*del , envelope_path_labels ,      , nopr
*dim , envelope_path_labels , char , pathCount*2

*do,nthPath,1,pathCount,1
  envelope_path_names(nthPath*2-1)  = path_names(nthPath)
  envelope_path_names(nthPath*2)    = path_names(nthPath)
  envelope_path_labels(nthPath*2-1) = 'NET_UZ+'
  envelope_path_labels(nthPath*2)   = 'NET_UZ-'
*enddo

view1w
wincln,1
/view,1,1,1,1
/plopts,leg3,on
g_annobold,1
wntitle , 'ENCONTRO 0%arg1%:'     , 1 , 0
wntitle , 'LAJE DE APROX.'   , 1 , 0 , 'C'
wntitle , 'FLECHA RARA [mm]' , 1 , 1 , 'R'
/triad,off
/eshape,1
/udoc,1,cntr,right
/edge,1,0
cmsel,s,COMPA_ABT_appSlab
cmsel,r,COMPA_ABT_%arg1%
/auto,1
aplot
/user,1
cmsel,s,COMPA_ABT_%arg1%
cmsel,u,COMPA_ABT_appSlab
esla,s
nsle,s,active

go2png,1200
  /device,vector,on
  eplot
  /noerase
  /user,1
  cmsel,s,COMPL_ABT_piles
  esll,a
  nsle,s,active
  eplot
  cmsel,s,COMPA_ABT_appSlab
  cmsel,r,COMPA_ABT_%arg1%
  esla,s
  nsle,s,active
  /device,vector,off
  /eshape,0
  /edge,1,0
  eplot
  cleanplot,'mpathplot','3',,'envelope_path_names','envelope_path_labels'
endpng,'IMG18a_ABT%arg1%_APPSLAB_deflects',,,,,'memo_imgs'
/erase
/annot,dele
clims
