! BRIDGE_V1_PARMESO

! Exported params:
! GEO_colLength
! GEO_columnY_[f]
! GEO_frameGirderEndsY_[f]
! GEO_frameGirderY_[f]
! GEO_MESO[s]_alpha
! GEO_MESO[s]_x
! GEO_MESO[s]_y
! GEO_midspanChi
! GEO_midspanX
! GEO_spanCount
! GEO_spanLengths
! GEO_supportChi
! GEO_supportChi_0
! GEO_supportChi_L
! GEO_supportX
! GEO_waterHeight
! zFrameBot
! zFrameTop


!! A.  Calculate key span & support positions                                                     !!
GEO_spanCount = GEO_supportCount-1  ! TODO  possibly rename param ("midSpanCount"?) to avoid confusing with span count that includes abutment approach slabs

*del , GEO_spanLengths ,  , nopr
*del , GEO_midspanX    ,  , nopr
*dim , GEO_spanLengths ,  , GEO_spanCount
*dim , GEO_midspanX    ,  , GEO_spanCount

*if,GEO_supportOpt,eq,0,then
  *del,GEO_supportX,,nopr
  *dim,GEO_supportX,,GEO_supportCount
  supportDist = GEO_deckLength/GEO_spanCount
  *vfill,GEO_supportX,ramp,0,supportDist
*endif

*do,nthSpan,1,GEO_spanCount,1
  ithX                     = GEO_supportX(nthMid)
  jthX                     = GEO_supportX(nthMid+1)
  GEO_spanLengths(nthSpan) = abs(jthX-ithX)
  GEO_midspanX(nthMid)     = (ithX+jthX)/2
*enddo

BRIDGE_V1_XTOCHI , 'GEO_supportChi' , 'GEO_supportX'
BRIDGE_V1_XTOCHI , 'GEO_midspanChi' , 'GEO_midspanX'
GEO_supportChi_0 = GEO_supportChi(1)                 ! TODO  move elsewhere, used only twice
GEO_supportChi_L = GEO_supportChi(GEO_supportCount)  ! TODO  move elsewhere, used only twice


!! B.  Mesostructure anchor positions                                                             !!
afunbackup
*afun,deg
*do,nthSupport,1,GEO_supportCount,1
  xCenter = GEO_supportX(nthSupport)
  *if,GEO_deckRadius,eq,0,then
    yOffset                    = GEO_deckOff_beg+(GEO_deckOff_end-GEO_deckOff_beg)*(xCenter/GEO_deckLength)
    GEO_MESO%nthSupport%_x     = xCenter
    GEO_MESO%nthSupport%_y     = yOffset+(GEO_deckWidth_beg/2)
    GEO_MESO%nthSupport%_alpha = 0
  *else
    chi_i                      = GEO_supportChi(nthSupport)
    GEO_MESO%nthSupport%_x     = xCenter
    GEO_MESO%nthSupport%_y     = GEO_deckRadius*sin(chi_i)-GEO_chordOff
    GEO_MESO%nthSupport%_alpha = 90-chi_i
  *endif
*enddo
afunrestore


!!                                                                                                !!
! TODO: check temporarily removed (has to be redesigned for skewed multi-span bridges)
! ! Check width of the frame
! leftGirderY=GEO_girderY_beg(GEO_girderCount)
! rightGirderY=GEO_girderY_beg(1)
! minFrameWidth=rightGirderY-leftGirderY
! *if,GEO_frameWidth,LT,minFrameWidth,THEN
!    GEO_frameWidth=minFrameWidth
!    *msg,GEO_frameWidth
! WARNING: Frame width incompatible with girder layout. GEO_FRAMEWIDTH increased to %G.
! *endif
!!                                                                                                !!


!! C.  Pier frame key positions                                                                   !!
! Vertical positions
GEO_colLength = GEO_frameHeight
*if,SEC_offset,EQ,1,THEN
   GEO_colLength = GEO_frameHeight-FRAMEGIRDER_h
*endif

zFrameTop = 0
*if,SEC_offset,eq,1,then
  zFrameTop = 0-GIRDER_d(1)-FRAMEGIRDER_h !! TODO: contemplate multiple girder heights
*endif

zFrameBot = zFrameTop-GEO_colLength

*if,GEO_waterHeight,ge,GEO_colLength,then
  GEO_waterHeight = GEO_colLength-0.1
  *msg,warn,GEO_waterHeight
WARNING: Water level incompatible with frame height. GEO_WATERHEIGHT reduced to %G.
*endif

! Transversal positions
yCenterBeg     = GEO_deckOff_beg+(GEO_deckWidth_beg/2)
yCenterEnd     = GEO_deckOff_end+(GEO_deckWidth_end/2)
yCenterDelta   = yCenterEnd-yCenterBeg
deckWidthDelta = GEO_deckWidth_end-GEO_deckWidth_beg

*do,nthFrame,1,GEO_supportCount,1

  xSupport            = GEO_supportX(nthFrame)
  skewnessScaleFactor = xSupport/GEO_deckLength
  deckWidth           = deckWidthDelta*skewnessScaleFactor+GEO_deckWidth_beg
  yCenter             = yCenterDelta*skewnessScaleFactor+yCenterBeg

  ! Frame girder
  *del , GEO_frameGirderEndsY_%nthFrame% ,  , nopr
  *del , GEO_columnY_%nthFrame%          ,  , nopr
  *dim , GEO_frameGirderEndsY_%nthFrame% ,  , 2
  *dim , GEO_columnY_%nthFrame%          ,  , 2

  GEO_frameGirderEndsY_%nthFrame%(1) = yCenter-(deckWidth/2)         , yCenter+(deckWidth/2)
  GEO_columnY_%nthFrame%(1)          = yCenter-(GEO_columnSpacing/2) , yCenter+(GEO_columnSpacing/2)

  ! Longitudinal girders
  *del,girder_y,,nopr
  *vfact,1,1-skewnessScaleFactor,skewnessScaleFactor
  *voper,girder_y,GEO_girderY_beg,add,GEO_girderY_end

  ! Compose key Y-location array with the following positions:
  !    left & right columns
  !    left & right extremities
  !    girder intersections
  arrjoin,'y_coords','girder_y','GEO_frameGirderEndsY_%nthFrame%','GEO_columnY_%nthFrame%'
  arruniq,'y_coords','GEO_frameGirderY_%nthFrame%',1

  ! TODO: Currently using deck width at support as frame width.
  !       For a permanent solution, frame width has to be given at each support.

*enddo
