! BRIDGE_V1_ABT6SLICE , abtCS
! BRIDGE_V1_ABT6SLICE , ARG1

! Dependencies:
!   GEO_ABT_appSlab
!   GEO_ABT_appSlabW
!   GEO_ABT_columnSpacing
!   GEO_waterHeight
!   zFrameBot
!
!   cml_columns
!   cml_frameGirder
!   cml_posts
!   cma_approachSlab
!   cma_cantileverWall


! Prepare environment
csys,ARG1

! Connect frame girder, columns & posts
lsel,none
cmsel,a,cml_posts
cmsel,a,cml_columns
cm,cml_vertical,line
cmsel,a,cml_frameGirder
lsbl,cml_frameGirder,cml_vertical,,delete,keep
cmsel,u,cml_vertical
cm,cml_frameGirder,line
cmdele,cml_vertical

! Slice approach slab, frame girder & wall at superstructure positions
BRIDGE_V1_LONGSLICE , 'cma_cantileverWall'
BRIDGE_V1_LONGSLICE , 'cma_approachSlab'
BRIDGE_V1_LONGSLICE , 'cml_frameGirder'

! Slice cantilever wall & frame girder at additional approach slab vertices: left & right edges
*if,GEO_ABT_appSlab,eq,1,then
  wpcsys,-1
  wpoffs,,-GEO_ABT_appSlabW/2
  wprota,,90
  cmwslice,'cma_cantileverWall'
  cmwslice,'cml_frameGirder'
  wpcsys,-1
  wpoffs,,GEO_ABT_appSlabW/2
  wprota,,90
  cmwslice,'cma_cantileverWall'
  cmwslice,'cml_frameGirder'
*endif

! Slice cantilever wall & approach slab at posts & columns  TODO  hard-coded for 2 columns
wpcsys,-1
wpoffs,,-GEO_ABT_columnSpacing/2
wprota,,90
cmwslice,'cma_cantileverWall'
cmwslice,'cma_approachSlab'
wpcsys,-1
wpoffs,,GEO_ABT_columnSpacing/2
wprota,,90
cmwslice,'cma_cantileverWall'
cmwslice,'cma_approachSlab'

cmsel,s,cml_posts
ksll,s
cm,cmk_posts,kp
kpNum = 0
*get,kc,kp,,count

*do,nthPost,1,kc,1
  cmsel,s,cmk_posts
  kpNum = kpnext(kpNum)
  wpcsys,-1
  kwpave,kpNum
  wprota,,90
  cmwslice,'cma_cantileverWall'
  cmwslice,'cma_approachSlab'
*enddo

! Slice columns at water level  TODO  this doesn't quite work because 'zFrameBot' is only ever
!                                     properly defined for non-abutment mesostructure -- water level
!                                     approach needs to be redefined
wpcsys,-1
wpoffs,,,zFrameBot+GEO_waterHeight
cmwslice,'cml_columns'
